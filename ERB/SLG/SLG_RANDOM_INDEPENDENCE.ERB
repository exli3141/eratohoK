;----------------------------------------------
;謀反を起こすNPCの候補を選択し、確率ロール
;----------------------------------------------
@SLG_RANDOM_INDEPENDENCE
#DIM LCOUNT
#DIM 人数
#DIM 候補, 3000
#DIM 候補値, 3000
#DIM 結果
#DIM 判定値
VARSET 人数
VARSET 候補
VARSET 候補値
;10期以降
SIF !(DAY >= 10)
	RETURN 0

;---今回の謀反候補を取得---
FOR LCOUNT, 1, CHARANUM
	SIF !CAN_RANDOM_INDEPENDENCE(LCOUNT)
		CONTINUE
	候補:人数 = LCOUNT
	;選出候補値は野心をそのまま利用
	候補値:人数 = RAND(0, MAX(1, ABL:LCOUNT:野心))
	人数 ++
NEXT

;候補がいなければRETURN
SIF 人数 < 1
	RETURN 0

LOCAL = MAXARRAY(候補値, 0, 人数)
FOR LCOUNT, 0, 人数
	IF 候補値:LCOUNT == LOCAL
		結果 = 候補:LCOUNT
		BREAK
	ENDIF
NEXT
;---候補取得ここまで---

判定値 = CALC_INDEPENDENCE_RATE(結果)

;確率ロールに失敗したら戻る
IF 判定値 <= RAND:10000
	RETURN 0
ENDIF
CALL NPC_INDEPENDENCE(結果)

;----------------------------------------------
;謀反できる状態か
;----------------------------------------------
@CAN_RANDOM_INDEPENDENCE(対象)
#FUNCTION
#DIM 対象
;無所属でない／いずれかの勢力の捕虜ではない／放浪もしくは死亡状態ではないか
SIF !IS_COUNTRY(CFLAG:対象:所属) || CFLAG:対象:捕虜先 || CFLAG:対象:特殊状態
	RETURNF 0
;さすがに君主が謀反起こしたらダメですよね。
SIF GET_COUNTRY_BOSS(CFLAG:(対象):所属) == 対象
	RETURNF 0
;SPキャラも謀反を起こさない
SIF IS_SP_CHARA(対象)
	RETURNF 0
;特殊勢力所属なら駄目
SIF IS_SP_COUNTRY(CFLAG:対象:所属)
	RETURNF 0
;野心が皆無なやつも謀反を起こさない
SIF ABL:対象:野心 <= 0
	RETURNF 0
;主人公が君主なら、IS_SLAVEないしIS_LOVERのキャラは謀反しない
SIF CFLAG:対象:所属 == CFLAG:MASTER:所属 && GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER && (IS_LOVER(対象) || IS_SLAVE(対象))
	RETURNF 0
RETURNF 1

;----------------------------------------------
;謀反を起こす確率を算出
;----------------------------------------------
@CALC_INDEPENDENCE_RATE(対象)
#FUNCTION
#DIM 対象
#DIM 判定値
;野心とREL_LIKE/HATE、所有都市数、DAYから判定値を算出
判定値 = ABL:対象:野心
判定値 -= REL_LIKE:対象:(GET_COUNTRY_BOSS(CFLAG:対象:所属)) / 30
判定値 += REL_HATE:対象:(GET_COUNTRY_BOSS(CFLAG:対象:所属)) / 50
;都市数が多いほど離反しやすい
判定値 += GET_OWN_CITY(CFLAG:対象:所属) * 2
;終盤は離反しにくくする（ただでさえ終わらない観戦モードがさらに終わらなくなるので）
判定値 -= LIMIT(DAY / 2 - 50, 0, 200)
SIF TALENT:対象:臆病
	判定値 -= 5
SIF TALENT:対象:反抗的
	判定値 += 15
SIF TALENT:対象:素直
	判定値 -= 8
SIF TALENT:対象:大人しい
	判定値 -= 5
SIF TALENT:対象:プライド高い
	判定値 += 20
SIF TALENT:対象:生意気
	判定値 += 10
SIF TALENT:対象:プライド低い
	判定値 -= 15
SIF TALENT:対象:自制心
	判定値 -= 5
SIF TALENT:対象:保守的
	判定値 -= 5
SIF TALENT:対象:好奇心
	判定値 += 5
SIF TALENT:対象:一線越えない
	判定値 -= 15
SIF TALENT:対象:抑圧
	判定値 -= 5
SIF TALENT:対象:解放
	判定値 += 15
SIF TALENT:対象:孤高
	判定値 += 20

;3能力が200越えてたらプラス補正
SIF ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 > 250
	判定値 += MAX(ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 - 200, 50)

;対象キャラがMASTERと同一勢力に所属していた場合は好感度と従属度で補正
SIF CFLAG:対象:所属 == CFLAG:MASTER:所属
	判定値 -= MIN((CFLAG:対象:好感度 / 500 + CFLAG:対象:従属度 / 500), 100)

RETURNF 判定値
