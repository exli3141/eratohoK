;-------------------
;●風紀の乱れ
;-------------------
@REGISTER_TERRITORY_EVENT34(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT34(対象)
#DIM 対象
#DIM 対象都市
CALL DAILY_EVENT_RAND_CITYSELECT(0)
対象都市 = RESULT
SIF 対象都市 < 0
	RETURN 0
PRINTFORML %CITY_NAME:対象都市%の風紀が乱れているようだ
PRINTFORML しかしそれによって経済は発展しているようだ
PRINTFORML 戦争のストレス発散の為に目をつぶるべきだろうか？
PRINTFORMW どうしよう？
CALL ASK_YN("厳しく取り締まる", "多少は目をつぶる")
IF RESULT == 0
	PRINTFORML 風紀の乱れは指揮の乱れにつながる
	PRINTFORMW %ANAME(対象)%は厳しく取り締まるように指示した
ELSE
	PRINTFORMW 戦争の不満をそらすにもある程度は目をつぶろう
	PRINTFORML 
	PRINTFORMW …%CITY_NAME:対象都市%の経済は潤った
	LOCAL:0 = RAND(CITY_ECONOMY_LIMIT:(対象都市) / 100 * 100 / 10 , CITY_ECONOMY_LIMIT:(対象都市) / 100 * 100 / 10 + 1000)
	IF CITY_ECONOMY:対象都市 == CITY_ECONOMY_LIMIT:対象都市
		CITY_ECONOMY_LIMIT:対象都市 += LOCAL:0
		CALL COLORPRINT(@"%CITY_NAME:対象都市%の経済規模の上限が{LOCAL:0 / 100}上昇し、{CITY_ECONOMY_LIMIT:対象都市 / 100}になった！", カラー_注意, "W")
	ELSE
		IF CITY_ECONOMY_LIMIT:対象都市 < (CITY_ECONOMY:対象都市 + LOCAL:0)
			LOCAL:0 = CITY_ECONOMY_LIMIT:対象都市 - CITY_ECONOMY:対象都市
		ENDIF
		CITY_ECONOMY:対象都市 += LOCAL:0
		CALL COLORPRINT(@"%CITY_NAME:対象都市%の経済規模が{LOCAL:0 / 100}上昇し、{CITY_ECONOMY:対象都市 / 100}になった！", カラー_注意, "W")
	ENDIF
	FOR LOCAL, 1, MAX_COUNTRY
		CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -30, 30)
	NEXT
	CALL COLORPRINT(" しかし治安が悪化し国の評判は下がった", カラー_注意, "W")
ENDIF
RETURN 1

