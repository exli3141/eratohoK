;-------------------
;●古びた矛を拾う
;-------------------
@REGISTER_TERRITORY_EVENT2(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT2(対象)
#DIM 対象
#DIM 金額
PRINTFORML 探索中、古びた矛を見つけた
PRINTFORML 長い間放置されていた様だが、磨けばまだ使えるかもしれない
PRINTFORMW どうしよう？
CALL ASK_MULTI("持ち帰って磨く", "武器屋に売る", "捨てておく")
IF RESULT == 2
	PRINTFORML こんなボロボロの武器には用はない
	PRINTFORMW そう思った%ANAME(対象)%は矛を放り投げると宮殿へ戻った
ELSEIF RESULT == 1
	PRINTFORMW こんなものでも武器だ、もしかしたら高値で売れるかもしれない
	PRINTFORMW %ANAME(対象)%は武器屋へ持って行った
	金額 = MIN(3000 + 1000 * (DAY / 10), 10000)
	MONEY += 金額
	CALL COLORPRINT(@"矛は金{金額}で売れた！", カラー_注意, "W")
	PRINTFORMW 思わぬ臨時収入に%ANAME(対象)%はホクホク顔で宮殿へ戻った
ELSE
	PRINTFORML 折角拾った武器だ、持ち帰ってみよう
	PRINTFORML そう思った%ANAME(対象)%は矛を抱えて宮殿へ戻った
	PRINTFORMW 宮殿に戻ると早速矛を磨いてみた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORMW ・
	IF RAND:3 == 0
		PRINTFORML すると矛は立派な輝きを取り戻した！
		PRINTFORMW 手に持ってみると力がみなぎるのを感じた
		CALL PRINT_ADD_EXP(対象, "武闘経験値", RAND:10 + 1, 1)
	ELSE
		PRINTFORML そこら辺にあるような普通の矛だった
		PRINTFORMW 自分には不要だと思い、部下に与えることにした
	ENDIF
ENDIF
RETURN 1

