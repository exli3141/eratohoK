;-------------------
;●鬼に挑まれる
;-------------------
@REGISTER_TERRITORY_EVENT25(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT25(対象)
#DIM 対象
#DIM 仲間
#DIM 兵数
PRINTFORML 郊外を歩いていると、目の前に鬼が立ちはだかった
PRINTFORML %ANAME(対象)%の噂を聞いてやって来たらしい、力試しがしたいとのことだ
PRINTFORMW どうしよう？
CALL ASK_YN("力試しをする", "断る")
IF RESULT == 1
	PRINTFORML 断ると鬼は臆病者と罵って去って行った
	PRINTFORMW %ANAME(対象)%は失礼な奴だと憤りながら宮殿へ戻った
ELSE
	PRINTFORMW 挑戦を受けると鬼は嬉しそうに笑い早速かかってきた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORMW ・
	IF ABL:対象:武闘 > 70 + ((DAY / 10) * (RAND:10 + 1))
		PRINTFORML 見事鬼を打倒した！
		PRINTFORML 鬼は%ANAME(対象)%の力を認め、深々と頭を下げると手下ともども仲間にしてほしいと言ってきた
		PRINTFORMW 仲間が増えるのは大歓迎だ、受け入れてやることにした
		兵数 = MIN(1000 * (DAY / 10), 10000)
		SIF 兵数 == 0
			兵数 = 1000
		COUNTRY_SOLDIER:(CFLAG:対象:所属) += 兵数
		PRINTFORMW 兵数が{兵数}増えた
	ELSE
		PRINTFORML 鬼は想像以上に強く、負けてしまった
		PRINTFORMW 地面に横たわる%ANAME(対象)%を見下ろすと鬼は首を振りながら去って行った
		CALL ADD_COOLTIME(対象:0, 3)
	ENDIF
ENDIF
RETURN 1

