;-------------------------------------------------
;「領地探索」の名称
;-------------------------------------------------
@SHOP_SLG_NAME71
RESULTS:0 '= "領地探索"

;-------------------------------------------------
;「領地探索」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK71
SIF FLAG:観戦モード || CFLAG:MASTER:所属 == 0
	RETURN 0
SIF DONE_TERRITORYSEARCH == 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「領地探索」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY71
CALL TERRITORYSEARCH
RETURN 1

;-------------------------------------------------
;「領地探索」本体
;-------------------------------------------------
@TERRITORYSEARCH
#DIM 対象
#DIM イベント候補, 1000
#DIM イベント候補数
CALL SINGLE_DRAWLINE
PRINTFORML 手の空いている者に領地を探索させることにした
PRINTFORML 誰に行かせようか？
CALL SELECT_CHARA_LIST_SLG()
IF RESULT < 0
	PRINTFORMW やはりやめておこう
	RETURN
ENDIF
DONE_TERRITORYSEARCH = 1
対象 = RESULT
CALL SET_COOLTIME(対象, 1)
PRINTFORMW %ANAME(対象)%は領地探索に出かけた
PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・
DUMPRAND

VARSET イベント候補
VARSET イベント候補数

FOR LOCAL, 0, 300
	TRYCCALLFORM REGISTER_TERRITORY_EVENT{LOCAL}(対象)
		IF RESULT == 1
			イベント候補:イベント候補数 = LOCAL
			イベント候補数 ++
		ENDIF
	CATCH
	ENDCATCH
NEXT

IF イベント候補数 == 0
	CALL TERRITORY_EVENT_NOT_EXIST
	GOTO FINISHED
ENDIF


TRYCCALLFORM TERRITORY_EVENT{イベント候補:(RAND:イベント候補数)}(対象)
	CALL TRAIN_AUTO_ABLUP(対象)
CATCH
	CALL TERRITORY_EVENT_NOT_EXIST
ENDCATCH

$FINISHED
INITRAND

RETURN 

