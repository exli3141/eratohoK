;-------------------
;●呪いの人形
;-------------------
@REGISTER_TERRITORY_EVENT27(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT27(対象)
#DIM 対象
PRINTFORML 探索から帰る途中、道端にイタチの人形が落ちているのを見つけた
PRINTFORML なんだか気味の悪い人形だ…
PRINTFORMW どうしよう？
CALL ASK_MULTI("持ち帰る", "焼いてしまう", "放っておく")
IF RESULT == 0
	PRINTFORML 人形と目があった気がする
	PRINTFORML 吸い込まれるように人形に手が伸び、%ANAME(対象)%はそれを拾い上げると
	PRINTFORML 懐に大事にしまって宮殿へ戻った
	PRINTFORMW 人形を神棚に飾っておくと、なんだか仕事が捗るようになった気がした
	CALL PRINT_ADD_EXP(対象, "政治経験値", RAND:10 + 1, 1)
ELSEIF RESULT == 1
	PRINTFORML 嫌な予感がした%ANAME(対象)%はその場で人形を焼くことにした
	PRINTFORML 人形が火に包まれると、辺りにこの世のものとは思えない絶叫が響いた
	PRINTFORMW 火が収まるとそこには焼け残った動物の骨が残っていた…
ELSE
	PRINTFORMW 嫌な予感がした%ANAME(対象)%は人形を放置して足早に宮殿に戻った
	PRINTFORML 
	PRINTFORML 部屋に戻った%ANAME(対象)%は驚愕した！
	PRINTFORML 机の上に先ほどの人形が置いてあるではないか！
	PRINTFORML 誰かの悪戯かと思い、窓から投げ捨てたが次の日にはまた元の位置にあった
	PRINTFORMW …人形は何度捨てても気づいたらそばに戻って来ていた
	TALENT:対象:中毒しやすい = 1
	TALENT:対象:痛みに弱い = 1
	TALENT:対象:痛みに強い = 0
	CALL COLORPRINT(@"%ANAME(対象)%は[痛みに弱い][中毒しやすい]になった", カラー_注意, "W")
ENDIF
RETURN 1

