;-------------------
;●指輪を拾う
;-------------------
@REGISTER_TERRITORY_EVENT17(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT17(対象)
#DIM 対象
#DIM 仲間
#DIM 金額
PRINTFORML 探索中、地面に光る物を見つけた
PRINTFORML 土に埋もれたそれを拾い上げるときらりと光る指輪だった
PRINTFORML 誰かが落としたものだろう
PRINTFORMW どうしよう？
CALL ASK_MULTI_JUDGE("持ち主を探して届ける", 1,"仲間にプレゼントする", 対象 == MASTER,"ネコババする", 1,"捨てておく", 1)
IF RESULT == 3
	PRINTFORML よく見るとガラス玉の安物だ
	PRINTFORMW %ANAME(対象)%は指輪を放り投げると宮殿へ戻った
ELSEIF RESULT == 2
	PRINTFORML 良い物を拾った
	PRINTFORMW 店に売れば小遣いになるかもしれない
	金額 = 2000 * (RAND:5 + 1)
	CALL COLORPRINT(@"指輪は金{金額}で売れた", カラー_注意, "W")
	MONEY += 金額
ELSEIF RESULT == 1
	PRINTFORMW 誰にプレゼントしようかな？
	CALL SELECT_CHARA_LIST_SLG()
	仲間 = RESULT
	IF 仲間 == -1 || 仲間 == 対象
		PRINTFORMW いや、やっぱり売る事にしよう
		金額 = 1000 * (RAND:5 + 1)
		MONEY += 金額
		CALL COLORPRINT(@"指輪は金{金額}で売れた", カラー_注意, "W")
	ELSE
		CFLAG:仲間:好感度 += 150
		PRINTFORMW %ANAME(仲間)%に指輪をプレゼントすると喜んでもらえた
	ENDIF
ELSE
	PRINTFORML 指輪の持ち主は落として困ってるだろう
	PRINTFORML 持ち主を探すことにした
	PRINTFORML …無事に指輪の持ち主に届けることが出来た
	PRINTFORMW %ANAME(対象)%は良い気分で宮殿へ戻った
ENDIF
RETURN 1

