;-------------------
;●花畑
;-------------------
@REGISTER_TERRITORY_EVENT106(対象)
#DIM 対象
RETURN 1

@TERRITORY_EVENT106(対象)
#DIM 対象
#DIM 仲間
#DIM 候補キャラ, 1000
#DIM 候補数
VARSET 候補キャラ
VARSET 候補数
FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):所属 == CFLAG:MASTER:所属 && !CFLAG:(LOCAL):捕虜先 && !IS_ANIMAL(LOCAL)
		候補キャラ:LOCAL = RAND:1000 
		候補数 ++
	ENDIF
NEXT
IF 候補数 == 0
	仲間 = 0
ELSE
	仲間 = FINDELEMENT(候補キャラ, MAXARRAY(候補キャラ))
ENDIF
PRINTFORML 探索中、素敵な花畑を見つけた
PRINTFORML 妖精たちが楽しげに踊っている
PRINTFORMW どうしよう？
CALL ASK_MULTI("寝転ぶ" ,"花を摘んでいく" ,"歌う")
IF RESULT == 0
	PRINTFORML %ANAME(対象)%はその場にごろりと寝転んだ
	PRINTFORMW 心地よい花の香りに包まれて、ついつい眠気に誘われた
	PRINTFORML 
	PRINTFORML 寝苦しさに目を開けると、妖精たちが%ANAME(対象)%に群がって眠っていた
	PRINTFORML 彼女たちも%ANAME(対象)%に釣られて昼寝をしていたらしい
	PRINTFORML 苦笑しながら体を起こすと、彼女たちも目を覚ました
	PRINTFORML 空を見上げるとすっかり夕焼け空になっていた
	PRINTFORMW 無邪気に手を振る彼女たちと別れて帰路についた
ELSEIF RESULT == 2
	PRINTFORMW 気分がよくなり一曲歌いたくなった%ANAME(対象)%は、歌いだした
	PRINTFORML 
	IF (ABL:対象:歌唱 / 10) * (RAND:5 + 1) >= (RAND:10 + 3)
		PRINTFORML いつの間にか妖精たちに囲まれていた
		PRINTFORMW 歌が終わると、彼女たちから拍手を貰えた
		CALL PRINT_ADD_EXP(対象, "歌唱経験値", RAND:10 + 6, 1)
	ELSE
		PRINTFORML 歌い終えた%ANAME(対象)%は、辺りから妖精たちの姿が消えていることに気付く
		PRINTFORML 首をかしげていると周囲の茂みからブーイングが飛んできた
		PRINTFORMW %ANAME(対象)%はショックを受けて逃げるように走り去った
	ENDIF
ELSE
	PRINTFORML 折角なので花を摘んでいくことにした
	PRINTFORMW 妖精たちにも手伝ってもらえて素敵な花束が出来た
	IF 対象 == MASTER && 仲間 != 0
		PRINTFORML
		PRINTFORMW 帰り道で出会った%ANAME(仲間)%に渡したら喜ばれた
		CFLAG:仲間:好感度 += 200
		CFLAG:仲間:面識 = 1
	ENDIF
ENDIF
RETURN 1

