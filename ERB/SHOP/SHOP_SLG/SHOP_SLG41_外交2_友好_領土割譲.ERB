;-------------------------------------------------
;領土を割譲する
;-------------------------------------------------
@DIPLOMACY_GIVE_CITY(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
CALL TMP_CREATE_UNIT_MAP
CALL TMP_CREATE_RELATION_MAP
;自国の都市の数を数える
LOCAL:6 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属 && GET_CITYNAME(LOCAL:0) != "無名" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 1
	PRINTFORMW 割譲できる領土がありません
	RETURN 0
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML 割譲する都市を選択して下さい
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE
PRINTBUTTON "[戻る]", 0


$INPUT_LOOP
INPUT

SIF RESULT == 0
	RETURN 0


LOCAL:15 = (RESULT - 1000)

IF LOCAL:15 >= 0 && CITY_OWNER:(LOCAL:15) == CFLAG:MASTER:所属
	
	;対象キャラの情報を表示
	CALL SINGLE_DRAWLINE
	CALL SHOW_CITY_INFO(LOCAL:15)
	CALL SINGLE_DRAWLINE
	PRINTFORML この領土を割譲しますか？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF

	PRINTFORMW %GET_CITYNAME(LOCAL:15)%は%ANAME(LOCAL:5)%勢力の領土になりました
	CALL SINGLE_MINIMIZE(LOCAL:15)
	CITY_OWNER:(LOCAL:15) = ARG:0

	;好感度上昇値・敵対値下降値の計算
	LOCAL:25 = CITY_ECONOMY:(LOCAL:15) / 100
	LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

	;政治・料理の補正
	CALL TMP_CREATE_IS_FREE_MAP
	LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
	LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
	LOCAL:25 = LOCAL:25 * (LOCAL:0 / 10 + LOCAL:1 / 10) / 10

	;互いの国同士の好感度を上昇させ敵対値を低下させる
	CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
	RETURN 1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

