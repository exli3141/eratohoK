;-------------------------
;主人公　というか「あなた」をエディットする
;方針　MASTER_EDITは一度仮想的にキャラを生成し、そのキャラのステータスをいじる
;最終的に出来上がったキャラを「あなた」としてすげかえる
;-------------------------
@MASTER_EDIT(対象)
#DIM 対象
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT
REDRAW 0
$SHOW_LOOP
CALL MASTER_EDIT_UI(対象)

INPUT

SELECTCASE RESULT
	;名前
	CASE 0 TO 10
		CALL MASTER_EDIT_PROCESS_NAME(対象, RESULT)
	;種族
	CASE 4000 TO 4999
		CALL MASTER_EDIT_PROCESS_TAG(対象, RESULT - 4000)
	;性別
	CASE 100 TO 110
		CALL MASTER_EDIT_PROCESS_SEX(対象, RESULT - 100)
	;性経験
	CASE 200 TO 210
		CALL MASTER_EDIT_PROCESS_SEX_EXP(対象, RESULT - 200)
	;能力
	CASE 300 TO 310
		CALL MASTER_EDIT_PROCESS_SLG_ABL(対象, RESULT - 300)
	;性的能力
	CASE 1000 TO 1999
		CALL MASTER_EDIT_PROCESS_SEX_ABL(対象, RESULT - 1000)
	;素質
	CASE 2000 TO 2999
		CALL MASTER_EDIT_PROCESS_TALENT(対象, RESULT - 2000)
	;その他
	CASE 3000 TO 3999
		CALL MASTER_EDIT_PROCESS_MISC(対象, RESULT - 3000)
	;手動能力設定
	CASE 100001
		CALL MASTER_EDIT_SLG_ABL_EDIT(対象)
	CASE 100000
		REDRAW 1
		RETURN 対象
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------
;名前処理
;-------------------------
@MASTER_EDIT_PROCESS_NAME(対象, 番号)
#DIM 対象
#DIM 番号
SELECTCASE 番号
	CASE 0
		PRINTFORML 姓を入力
		INPUTS
		CSTR:対象:0 = %RESULTS%
	CASE 1
		PRINTFORML 名を入力
		INPUTS
		CSTR:対象:1 = %RESULTS%
		NAME:対象 = %RESULTS%
		CALLNAME:対象 = %RESULTS%
	CASE 2
		PRINTFORML 愛称を入力
		INPUTS
		CSTR:対象:98 = %RESULTS%
ENDSELECT


;-------------------------
;タグ処理
;-------------------------
@MASTER_EDIT_PROCESS_TAG(対象, 番号)
#DIM 対象
#DIM 番号
SIF !INRANGE(番号, 0, VARSIZE("TAG_NAME"))
	RETURN 0
CALL INVERT_TAG(対象, 番号)

;-------------------------
;性別処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX(対象, 番号)
#DIM 対象
#DIM 番号
TALENT:対象:性別 = 番号

;-------------------------
;性経験処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX_EXP(対象, 番号)
#DIM 対象
#DIM 番号
SELECTCASE 番号
	CASE 0
		TALENT:対象:童貞 = !TALENT:対象:童貞
		IF TALENT:対象:童貞
			SEXUAL_EXPERIENCE:対象:初体験_童貞 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_童貞 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_童貞 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_童貞 = 不明
		ENDIF
	CASE 1
		TALENT:対象:キス未経験 = !TALENT:対象:キス未経験
		IF TALENT:対象:キス未経験
			SEXUAL_EXPERIENCE:対象:初体験_キス = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_キス = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_キス = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_キス = 不明
		ENDIF
	CASE 2
		TALENT:対象:処女 = !TALENT:対象:処女
		IF TALENT:対象:処女
			SEXUAL_EXPERIENCE:対象:初体験_処女 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_処女 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_処女 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_処女 = 不明
		ENDIF
	CASE 3
		TALENT:対象:アナル処女 = !TALENT:対象:アナル処女
		IF TALENT:対象:アナル処女
			SEXUAL_EXPERIENCE:対象:初体験_アナル処女 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_アナル処女 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_アナル処女 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_アナル処女 = 不明
		ENDIF
ENDSELECT

;-------------------------
;SLG処理
;-------------------------
@MASTER_EDIT_PROCESS_SLG_ABL(対象, 番号)
#DIM 対象
#DIM 番号
#DIM 補正, 6
VARSET 補正

IF 番号 == 1
	TALENT:対象:成長型 = ROUND_INCREMENT(TALENT:対象:成長型, 0, 9)
ELSEIF 番号 == 0
	;期待値合計を270前後で
	SELECTCASE TALENT:対象:成長型
		;万能
		CASE 0
			ABL:対象:武闘 = RAND(40, 70)
			ABL:対象:防衛 = RAND(40, 70)
			ABL:対象:知略 = RAND(40, 70)
			ABL:対象:政治 = RAND(40, 70)
			ABL:対象:料理 = RAND(20, 30)
			ABL:対象:歌唱 = RAND(20, 30)
		;知将 武闘防衛知略
		CASE 1
			ABL:対象:武闘 = RAND(50, 80)
			ABL:対象:防衛 = RAND(50, 80)
			ABL:対象:知略 = RAND(50, 80)
			ABL:対象:政治 = RAND(30, 60)
			ABL:対象:料理 = RAND(10, 20)
			ABL:対象:歌唱 = RAND(10, 20)
		;学士 知略政治
		CASE 2
			ABL:対象:武闘 = RAND(30, 60)
			ABL:対象:防衛 = RAND(30, 60)
			ABL:対象:知略 = RAND(55, 80)
			ABL:対象:政治 = RAND(55, 80)
			ABL:対象:料理 = RAND(20, 30)
			ABL:対象:歌唱 = RAND(20, 30)
		;両道 武闘防衛政治
		CASE 3
			ABL:対象:武闘 = RAND(50, 80)
			ABL:対象:防衛 = RAND(50, 80)
			ABL:対象:知略 = RAND(30, 60)
			ABL:対象:政治 = RAND(50, 80)
			ABL:対象:料理 = RAND(10, 20)
			ABL:対象:歌唱 = RAND(10, 20)
		;武官 武闘防衛
		CASE 4
			ABL:対象:武闘 = RAND(70, 85)
			ABL:対象:防衛 = RAND(70, 85)
			ABL:対象:知略 = RAND(25, 55)
			ABL:対象:政治 = RAND(25, 55)
			ABL:対象:料理 = RAND(10, 25)
			ABL:対象:歌唱 = RAND(10, 25)
		;軍師 知略
		CASE 5
			ABL:対象:武闘 = RAND(30, 60)
			ABL:対象:防衛 = RAND(30, 60)
			ABL:対象:知略 = RAND(70, 85)
			ABL:対象:政治 = RAND(55, 70)
			ABL:対象:料理 = RAND(15, 25)
			ABL:対象:歌唱 = RAND(15, 25)
		;文官　政治
		CASE 6
			ABL:対象:武闘 = RAND(30, 60)
			ABL:対象:防衛 = RAND(30, 60)
			ABL:対象:知略 = RAND(55, 70)
			ABL:対象:政治 = RAND(70, 85)
			ABL:対象:料理 = RAND(15, 25)
			ABL:対象:歌唱 = RAND(15, 25)
		;芸能
		CASE 7
			ABL:対象:武闘 = RAND(30, 60)
			ABL:対象:防衛 = RAND(30, 60)
			ABL:対象:知略 = RAND(30, 60)
			ABL:対象:政治 = RAND(30, 60)
			ABL:対象:料理 = RAND(25, 35)
			ABL:対象:歌唱 = RAND(50, 70)
		;料理
		CASE 8
			ABL:対象:武闘 = RAND(30, 60)
			ABL:対象:防衛 = RAND(30, 60)
			ABL:対象:知略 = RAND(30, 60)
			ABL:対象:政治 = RAND(30, 60)
			ABL:対象:料理 = RAND(50, 70)
			ABL:対象:歌唱 = RAND(25, 35)
		;天才 期待値300
		CASE 9
			ABL:対象:武闘 = RAND(45, 75)
			ABL:対象:防衛 = RAND(45, 75)
			ABL:対象:知略 = RAND(45, 75)
			ABL:対象:政治 = RAND(45, 75)
			ABL:対象:料理 = RAND(25, 35)
			ABL:対象:歌唱 = RAND(25, 35)
	ENDSELECT
ENDIF

;-------------------------
;性的能力処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX_ABL(対象, 番号)
#DIM 対象
#DIM 番号
{
#DIM 編集可能能力 = 
GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｍ感"),
GETNUM(ABL, "性知識"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "奉仕"), GETNUM(ABL, "肝臓")
}

SIF FINDELEMENT(編集可能能力, 番号) == -1
	RETURN

IF 番号 == GETNUM(ABL, "性知識")
	PRINTL 値を入力(0~5)
	INPUT
	ABL:対象:性知識 = LIMIT(RESULT, 0, 5)
ELSE
	PRINTL 値を入力(0~3)
	INPUT
	ABL:対象:番号 = LIMIT(RESULT, 0, 3)
ENDIF

;-------------------------
;素質処理
;-------------------------
@MASTER_EDIT_PROCESS_TALENT(対象, 番号)
#DIM 対象
#DIM 番号
{
SIF !PASS_CHARA_CHECK_SHOW_TALENT(番号) || TALENTNAME:番号 == "" ||
GROUPMATCH(番号, GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "絶倫"), GETNUM(TALENT, "妖術知識"), GETNUM(TALENT, "天の御遣い"), GETNUM(TALENT,"技師"),
				GETNUM(TALENT, "酒豪"), GETNUM(TALENT, "君主の器"), GETNUM(TALENT, "求心力"))
}
	RETURN

TALENT:対象:番号 = !TALENT:対象:番号

;-------------------------
;そのほか処理
;-------------------------
@MASTER_EDIT_PROCESS_MISC(対象, 番号)
#DIM 対象
#DIM 番号

SELECTCASE 番号
	CASE 0
		CALL SET_BUSTSIZE(対象, ROUND_INCREMENT(GET_BUSTSIZE(対象), -2, 2))
	CASE 1
		CALL SET_PENISSIZE(対象, ROUND_INCREMENT(GET_PENISSIZE(対象), -2, 2))
	CASE 100
		TALENT:対象:Ｖ締まり += 100
		SIF TALENT:対象:Ｖ締まり > 1000
			TALENT:対象:Ｖ締まり = 50
	CASE 101
		TALENT:対象:Ａ締まり += 100
		SIF TALENT:対象:Ａ締まり > 1000
			TALENT:対象:Ａ締まり = 50
	CASE 200
		TALENT:対象:陰毛現在値 = ROUND_INCREMENT(TALENT:対象:陰毛現在値, -1, 10)
		TALENT:対象:陰毛目標値 = TALENT:対象:陰毛現在値
	CASE 300
		PRINTL 髪の長さを入力
		PRINTL ------長さの表記------
		PRINTL   2～ 24 スキンヘッド
		PRINTL  25～174 超ショート
		PRINTL 175～274 ショート
		PRINTL 275～474 ミドル
		PRINTL 475～674 ロング
		PRINTL 675～999 超ロング
		INPUT
		TALENT:対象:髪の長さ = LIMIT(RESULT, 2, 999)
	CASE 301
		TALENT:対象:髪色 = RAND:0xFFFFFF
	CASE 302
		TALENT:対象:髪色 = 0
	CASE 303
		PRINTL 髪色のカラーコードを入力(0xは不要)
		INPUTS
		SIF ISNUMERIC("0x" + RESULTS) && INRANGE(TOINT("0x" + RESULTS), 0, TOINT("0xFFFFFF"))
			TALENT:対象:髪色 = TOINT("0x" + RESULTS)
ENDSELECT

;-------------------------
;UI部分
;-------------------------
@MASTER_EDIT_UI(対象)
#DIM 対象
CALL SINGLE_DRAWLINE
PRINTFORML 主人公の素質や能力を設定します

CALL SINGLE_DRAWLINE
PRINTFORML 名前
CALL MASTER_EDIT_NAME(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 種族
CALL MASTER_EDIT_TAG(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性別
CALL MASTER_EDIT_SEX(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性経験
CALL MASTER_EDIT_SEX_EXP(対象)

CALL SINGLE_DRAWLINE
PRINTFORML SLG能力
CALL MASTER_EDIT_SLG_ABL(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性的能力
CALL MASTER_EDIT_SEX_ABL(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 素質
CALL MASTER_EDIT_TALENT(対象)

CALL SINGLE_DRAWLINE
PRINTFORML その他
CALL MASTER_EDIT_MISC(対象)

CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 100000

;-------------------------
;名前UI
;-------------------------
@MASTER_EDIT_NAME(対象)
#DIM 対象
PRINTFORM   姓 - %CSTR:対象:0, 20, LEFT%
PRINTBUTTON "[入力]", 0
PRINTL 
PRINTFORM   名 - %CSTR:対象:1, 20, LEFT%
PRINTBUTTON "[入力]", 1
PRINTL 
PRINTFORM 愛称 - %CSTR:対象:98, 20, LEFT%
PRINTBUTTON "[入力]", 2
PRINTL
PRINTFORML ※あまり長いと表示崩れします

;-------------------------
;タグUI
;-------------------------
@MASTER_EDIT_TAG(対象)
#DIM 対象
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 4000 + LOCAL, HAS_TAG(対象, LOCAL))
NEXT
PRINTL
PRINTFORML ※最大{MAX_TAG}個まで
PRINTFORML ※矛盾したタグが付与できる？　まぁ気にするなよ

;-------------------------
;性別UI
;-------------------------
@MASTER_EDIT_SEX(対象)
#DIM 対象
CALL PRINT_SELECT_BUTTON("[男]", 100, TALENT:対象:性別 == 0)
CALL PRINT_SELECT_BUTTON("[女]", 101, TALENT:対象:性別 == 1)
CALL PRINT_SELECT_BUTTON("[女双]", 102, TALENT:対象:性別 == 2)
CALL PRINT_SELECT_BUTTON("[男双]", 103, TALENT:対象:性別 == 3)
CALL PRINT_SELECT_BUTTON("[男娘]", 104, TALENT:対象:性別 == 4)
CALL PRINT_SELECT_BUTTON("[男娘双]", 105, TALENT:対象:性別 == 5)
PRINTL

;-------------------------
;性経験UI
;-------------------------
@MASTER_EDIT_SEX_EXP(対象)
#DIM 対象
PRINTBUTTON TALENT:対象:童貞 ? "[ 童貞 ]" # "[非童貞]", 200
PRINTBUTTON TALENT:対象:キス未経験 ? "[ キス未経験 ]" # "[キス経験済み]", 201
PRINTBUTTON TALENT:対象:処女 ? "[ 処女 ]" # "[非処女]", 202
PRINTBUTTON TALENT:対象:アナル処女 ? "[ Ａ処女 ]" # "[Ａ非処女]", 203
PRINTL

;-------------------------
;SLG能力UI
;-------------------------
@MASTER_EDIT_SLG_ABL(対象)
#DIM 対象
#DIM 表示能力 = GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理")

PRINTBUTTON "[変更]", 301
PRINTFORM 成長型:%TOSTR_GROWTYPE(TALENT:対象:成長型)%(%TOSTR_GROWTYPE_B(TALENT:対象:成長型)%に適性)
PRINTL
PRINTBUTTON "[手動設定]", 100001
PRINT   
PRINTBUTTON "[リロール]", 300
PRINT   
FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORM %ABLNAME:(表示能力:LOCAL)%:
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:(表示能力:LOCAL))
	PRINTFORM {ABL:対象:(表示能力:LOCAL), 3, RIGHT}  
NEXT
PRINTL

;-------------------------
;性的能力UI
;-------------------------
@MASTER_EDIT_SEX_ABL(対象)
#DIM 対象
{
#DIM 表示能力 = 
GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｍ感"),
GETNUM(ABL, "性知識"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "奉仕"), GETNUM(ABL, "肝臓")
}

FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORM %ABLNAME:(表示能力:LOCAL)%:
	IF 表示能力:LOCAL == GETNUM(ABL, "性知識")
		CALL PRINT_ALPHABET_RANK(ランク_性知識, ABL:対象:(表示能力:LOCAL))
	ELSE
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:(表示能力:LOCAL))
	ENDIF
	PRINTFORM {ABL:対象:(表示能力:LOCAL), 2, RIGHT}
	PRINTBUTTON "[設定]", 1000 + 表示能力:LOCAL
	PRINT   
	SIF LOCAL % 5 == 0 && LOCAL > 0
		PRINTFORML
NEXT
PRINTL
CALL KILL_EMPTY_LINE
;-------------------------
;素質UI
;-------------------------
@MASTER_EDIT_TALENT(対象)
#DIM 対象
LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF !PASS_CHARA_CHECK_SHOW_TALENT(LOCAL) || TALENTNAME:LOCAL == "" || GROUPMATCH(LOCAL, GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "絶倫"), GETNUM(TALENT, "妖術知識"), GETNUM(TALENT, "天の御遣い"), GETNUM(TALENT,"技師"), GETNUM(TALENT, "酒豪"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "君主の器"), GETNUM(TALENT, "求心力"))
		CONTINUE
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:LOCAL + "]", 14, LEFT%", 2000 + LOCAL, TALENT:対象:LOCAL)
	PRINT   
	LOCAL:1 ++
	SIF LOCAL:1 % 6 == 0 && LOCAL:1 > 0
		PRINTFORML
NEXT
PRINTL

;-------------------------
;その他UI
;-------------------------
@MASTER_EDIT_MISC(対象)
#DIM 対象

IF IS_FEMALE(MASTER)
	PRINTFORM バストサイズ:
	PRINTBUTTON @"[%TOSTR_BUSTSIZE(GET_BUSTSIZE(対象)), 10, LEFT%]", 3000
	PRINT     
ENDIF

IF HAS_PENIS(MASTER)
	PRINTFORM ペニスサイズ:
	PRINTBUTTON @"[%TOSTR_PENISSIZE(GET_PENISSIZE(対象)), 10, LEFT%]", 3001
	PRINTL 
ENDIF
IF HAS_VAGINA(MASTER)
	PRINTFORM     Ｖ締まり:
	PRINTBUTTON @"[%TOSTR_TIGHTNESS(TALENT:対象:Ｖ締まり), 10, LEFT%]", 3100
	PRINT     
ENDIF
PRINTFORM     Ａ締まり:
PRINTBUTTON @"[%TOSTR_TIGHTNESS(TALENT:対象:Ａ締まり), 10, LEFT%]", 3101
PRINTL 
PRINTFORM         陰毛:
PRINTBUTTON @"[%GET_INMOU_NAME(TALENT:対象:陰毛現在値)%]", 3200
PRINTL 

PRINTFORM           髪:%TOSTR_HAIR_LENGTH(TALENT:対象:髪の長さ), 12%({TALENT:対象:髪の長さ, 4})
PRINTBUTTON "[設定]", 3300
PRINTFORM   髪色:
CALL PRINT_HAIR_COLOR(TALENT:対象:髪色)
PRINTBUTTON "[ランダム]", 3301
IF TALENT:対象:髪色 != 0
	PRINT  
	PRINTBUTTON "[不明にする]", 3302
ENDIF
IF TALENT:対象:髪色 != 0
	PRINTBUTTON "[設定]", 3303
ENDIF
PRINTL

;-------------------------
;能力の手動設定
;-------------------------
@MASTER_EDIT_SLG_ABL_EDIT(対象)
#DIM 対象
#DIM 表示能力 = GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理")
#DIM ポイント
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

ポイント = 240
FOR LOCAL, 0, VARSIZE("表示能力")
	ポイント -= ABL:対象:(表示能力:LOCAL)
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML 能力を手動で設定します
PRINTFORML ポイントが許す限り、0～100で指定できます
PRINTFORML ただしリロールの期待値のほうが、ポイント合計より上です
PRINTFORML 残りポイント:{ポイント}
CALL SINGLE_DRAWLINE
FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORML [{LOCAL}] %ABLNAME:(表示能力:LOCAL)% - {ABL:対象:(表示能力:LOCAL)}
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [99] 戻る

INPUT

IF RESULT == 99
	IF ポイント < 0
		PRINTFORMW ポイント不足です
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
	RETURN 0
ELSEIF !INRANGE(RESULT, 0, VARSIZE("表示能力") - 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

LOCAL = RESULT
PRINTFORML %ABLNAME:(表示能力:LOCAL)%の能力値を入力(0~{MAX(ABL:対象:(表示能力:LOCAL) + ポイント, 0)})

$INPUT_LOOP

INPUT

SIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP

ABL:対象:(表示能力:LOCAL) = RESULT

CLEARLINE LINECOUNT - FIRST_LINE

RESTART
