;拉致

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME556
IF MTAR_NUM > 0 && TCVAR:(MTAR:0):催眠中 == 0
	RESULTS:0 = 催眠術をかける
	RESULTS:1 = 催眠術をかける
	RESULTS:2 = 催眠術をかける
	RESULTS:3 = 催眠術をかける
	RESULTS:4 = 催眠術をかける
	RESULTS:5 = 催眠術をかける
ELSE
	RESULTS:0 = 催眠術を解除
	RESULTS:1 = 催眠術を解除
	RESULTS:2 = 催眠術を解除
	RESULTS:3 = 催眠術を解除
	RESULTS:4 = 催眠術を解除
	RESULTS:5 = 催眠術を解除
ENDIF


;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE556
;共通部分
CALL COM_ABLE_COMMON(556)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;ターゲットが一人は必要
SIF MTAR_NUM <= 0
	RETURN 0
SIF FLAG:主導権所有者 != MASTER && FLAG:主導権所有者 != -1
	RETURN 0
LOCAL:1 = 0

FOR LOCAL, 0, MTAR_NUM
	;解除済みだと駄目
	SIF TCVAR:(MTAR:LOCAL):催眠中 == -1
		RETURN 0
	;解除する場合、まだかけていないやつがいると駄目
	SIF TCVAR:(MTAR:LOCAL):催眠中 > 0
		LOCAL:1 = 1
	SIF LOCAL:1 && TCVAR:(MTAR:LOCAL):催眠中 == 0
		RETURN 0
NEXT

SIF !ITEM:催眠術
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM556
;●実行値の計算
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤー側の処理

;ターゲット側の処理
FOR LOCAL, 0, MTAR_NUM
	IF TCVAR:(MTAR:LOCAL):催眠中 == 0
		TCVAR:(MTAR:LOCAL):催眠中 = 1
	ELSE
		TCVAR:(MTAR:LOCAL):催眠中 = -1
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1



;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE556
IF MTAR_NUM > 1
	LOCALS:0 = たち
ELSE
	LOCALS:0 = 
ENDIF
IF TCVAR:(MTAR:0):催眠中 > 0
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%%LOCALS:0%に催眠術をかけ%TARGET_STR("た", "てきた")%…
ELSE
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%%LOCALS:0%の催眠術を解除し%TARGET_STR("た", "てきた")%…
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN556
RETURN コマンド_共通
