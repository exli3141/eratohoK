;後背位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME31
IF IS_INSERT_SINGLE(MTAR:0, "Ａ") && IS_INSERT_MUTUAL(MPLY, MTAR:0) != 2
	LOCALS:0 = 二穴後背位
ELSE
	LOCALS:0 = 後背位
ENDIF

RESULTS:0 = %LOCALS:0%で挿入
RESULTS:1 = %LOCALS:0%で挿入させられる
RESULTS:2 = %LOCALS:0%で挿入させる
RESULTS:3 = %LOCALS:0%で挿入される
RESULTS:4 = %LOCALS:0%で挿入させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE31
;共通部分
CALL COM_ABLE_COMMON(31)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
;実行者に竿がなく、ペニスバンド装着中でもないと不可
SIF !HAS_PENIS(MPLY:0) && !IS_EQUIP_PLAYER(MPLY:0, 50)
	RETURN 0
;プレイヤーとターゲットの挿入関係を取得
LOCAL:0 = IS_INSERT_MUTUAL(MPLY:0, MTAR:0)
;ターゲットからプレイヤーに挿入中なら不可
SIF LOCAL:0 == 3 || LOCAL:0 == 4
	RETURN 0
;ターゲットのＶがプレイヤーの竿以外で埋まっているなら不可
SIF IS_V_HOLD(MTAR:0) && LOCAL:0 != 1
	RETURN 0
;ターゲットのＡが他のキャラの竿で埋まっているなら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && LOCAL:0 != 2
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが顔面騎乗中なら不可
SIF IS_RIDE(MPLY:0)
	RETURN 0
;ターゲットが顔面騎乗されているなら不可
SIF IS_RIDDEN(MTAR:0)
	RETURN 0
;ターゲットが顔面騎乗中なら不可
SIF IS_RIDE(MTAR:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 21) || IS_EQUIP_TARGET(MPLY:0, 21) || IS_EQUIP_PLAYER(MPLY:0, 22) || IS_EQUIP_TARGET(MPLY:0, 22)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 21) || IS_EQUIP_TARGET(MTAR:0, 21) || IS_EQUIP_PLAYER(MTAR:0, 22) || IS_EQUIP_TARGET(MTAR:0, 22)
	RETURN 0
;ターゲットがプレイヤーに胸愛撫中なら不可
SIF SEARCH_EQUIP(1, MTAR:0, MPLY:0) >= 0
	RETURN 0
;ターゲットがプレイヤー／ターゲットがプレイヤーに乳首吸い中なら不可
SIF SEARCH_EQUIP_IC(9, MTAR:0, MPLY:0) >= 0
	RETURN 0
;ターゲットがプレイヤーにＷ手コキ、Ｔ手コキ中なら不可
LOCAL:0 = SEARCH_EQUIP(10, MTAR:0, MPLY:0)
SIF LOCAL:0 >= 0 && MEQUIP_PLAYER_NUM:(LOCAL:0) >= 2
	RETURN 0
;プレイヤーが素股・足コキ・尻コキ・イラマ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 13, 14, 15, 90, 103)
	RETURN 0
;プレイヤーがフェラ・パイズリ・素股・足コキ・尻コキ・オナホコキ・電気按摩・触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MPLY:0, 11, 12, 13, 14, 15, 16, 103, 107, 205)
	RETURN 0
;プレイヤーがセルフオナホ中ならダメ
SIF IS_EQUIP_PLAYER(MPLY:0, 116)
	RETURN 0
;ターゲットがクンニされているなら不可
SIF IS_EQUIP_TARGET(MTAR:0, 2)
	RETURN 0
;ターゲットからプレイヤーにイラマ中なら不可
SIF SEARCH_EQUIP(90, MTAR:0, MPLY:0) >= 0
	RETURN 0
;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
SIF SEARCH_EQUIP_IC(104, MPLY:0, MTAR:0) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM31
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 3
		CASE 2
			TFLAG:17 = 1
		CASE 3
			TFLAG:17 = 3
		CASE 4
			TFLAG:17 = 2
		CASE 5
			TFLAG:17 = 3
		CASE 6
			TFLAG:17 = 2
		CASE 7
			TFLAG:17 = 2
		CASE 8
			TFLAG:17 = 3
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 3
		CASE 31
			TFLAG:17 = 1
		CASE 32
			TFLAG:17 = 3
		CASE 33
			TFLAG:17 = 2
		CASE 34
			TFLAG:17 = 3
		CASE 35
			TFLAG:17 = 2
		CASE 37
			TFLAG:17 = 2
		CASE 38
			TFLAG:17 = 3
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 160

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:0, MPLY:0, 1500)
SOURCE:(MPLY:0):性行動 = 450

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 20)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 200, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 120

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 1500)
SOURCE:(MTAR:0):露出 = 400
SOURCE:(MTAR:0):性行動 = 450

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 20)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 180)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:(MTAR:0):3 |= STAIN:(MPLY:0):2
STAIN:(MPLY:0):2 |= STAIN:(MTAR:0):3

;体位フラグのセット
TCVAR:(MPLY:0):31 = 2
TCVAR:(MTAR:0):33 = 2

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP31
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP31(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:2, 750)
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 750)
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE31(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の膣に後背位で挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP31(ARG:0)
	IF TALENT:(MEQUIP_TARGET:(ARG:0):0):処女 == 1
		LOCALS:0 = まだ乙女の
	ELSEIF PALAM:(MEQUIP_TARGET:(ARG:0):0):潤滑 >= PALAMLV:7
		LOCALS:0 = 濡れそぼった
	ELSE
		LOCALS:0 = 
	ENDIF
	IF IS_OVULATION(MEQUIP_TARGET:(ARG:0):0)
		LOCALS:0 = %LOCALS:0%危険日の
	ENDIF

	IF TALENT:(MEQUIP_TARGET:(ARG:0):0):小柄体型
		LOCALS:0 = %LOCALS:0%幼いワレメ
	ELSE
		LOCALS:0 = %LOCALS:0%秘裂
	ENDIF
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%の膣が%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥え込んでいる…
		CASE 1
			LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
			;ターゲットが行動不能
			IF !IS_PLAYABLE(LOCAL:0)
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は気絶した%EQUIP_TARGET_ANAME(ARG:0)%の膣を後ろから激しく突いている…
					CASE 1
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%はぐったりしている%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%に後ろから何度も腰を打ち付けている…
				ENDSELECT
			ELSE
				SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
					CASE 0
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は四つん這いの%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴み、激しく膣奥を突いている…
							CASE 1
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は後ろから%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%に突き入れ、何度も腰を打ち付けている…
						ENDSELECT
					;ターゲットに主導権
					CASE 1
						PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%に見せつけるように尻を振りながら、もっと激しく腰を動かすように要求し
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴みなおすと、後ろから何度も腰を打ち付けて%EQUIP_TARGET_ANAME(ARG:0)%の膣奥を突いている…
							CASE 1
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴みなおすと、後ろから何度も腰を打ち付け%LOCALS:0%に突き入れている…
						ENDSELECT
					;第三者に主導権
					CASEELSE
						PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_TARGET_ANAME(ARG:0)%に後背位で挿入している%EQUIP_PLAYER_ANAME(ARG:0)%に対し、もっと激しく腰を動かすように命令し
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は言われるまま腰の動きを激しくし、後ろから何度も%EQUIP_TARGET_ANAME(ARG:0)%の膣奥を突いている…
							CASE 1
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は言われるまま腰の動きを激しくし、後ろから何度も%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%に突き入れている…
						ENDSELECT
				ENDSELECT
			ENDIF
	ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP31(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER31(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 100

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE31
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):小柄体型
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF


IF TFLAG:17 == 1 && (PALAM:(MTAR:0):欲情 >= PALAMLV:10 || (PALAM:(MTAR:0):欲情 >= PALAMLV:7 && TCVAR:(MTAR:0):60)) && ABL:(MTAR:0):Ｖ感 >= ランク閾値:ランク_その他:ランク_C
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %ANAME(MTAR:0)%は情欲に流されるまま、おねだりするように%STR_BODY("尻：外見：修飾", MTAR:0)%お尻を突き出している…
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%は獣の姿勢で%BAR_NAME(MPLY:0)%を突き込まれながら、発情しきった雌そのものの顔で喘いでいる…
	ENDSELECT
ENDIF


;ペニスバンド装着中
IF IS_EQUIP_PLAYER(MPLY:0, 50)
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が後背位
				IF TFLAG:17 == 1
					SELECTCASE IFRAND("0, 2", 1, "3", ABL:(MPLY:0):主導度Ｕ >= 500)
						CASE 0
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いにした/四つん這いになった", 1)%%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：長", MTAR:0)%を%SPLIT_RAND("掴むと/鷲掴みにすると/揉みしだきながら/掴み直すと", 1, "抱え込むと", TALENT:(MTAR:0):豊尻)%、
						CASE 1
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いにした/四つん這いになった/挿入したまま", 1)%%ANAME(MTAR:0)%の%STR_BODY("腰", MTAR:0)%を%SPLIT_RAND("掴むと/掴み直すと/引き寄せると", 1)%、
						CASE 2
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いで繋がったままの/後背位で繋がったままの/尻を突き出した姿勢の", 1)%%ANAME(MTAR:0)%に背中から覆い被さると、
						CASE 3
							PRINTFORM %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：長", MTAR:0)%を%SPLIT_RAND("割り開いて/押し広げて/掴んで", 1)%%SPLIT_RAND("尻穴を/アナルを/結合部を/繋がった部分を", 1)%%SPLIT_RAND("剥き出しにしながら/眺めながら/観察しながら/丸見えにしながら", 1)%、
					ENDSELECT
					SELECTCASE IFRAND("0, 1", 1, "2", ABL:(MPLY:0):性交 >= ランク閾値:ランク_その他:ランク_C)
						CASE 0
							PRINTFORMW さらに激しく腰を動かして膣奥を突い%TARGET_STR("た", "てきた")%…
						CASE 1
							PRINTFORMW 音を立てて激しく腰を打ち付け%TARGET_STR("た", "てきた")%…
						CASE 2
							PRINTFORMW 張り型で弱い部分を激しく擦り上げ%TARGET_STR("た", "てきた")%…
					ENDSELECT
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押し倒して後ろから覆い被さると、後背位の体勢で再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を返して四つん這いにすると、後背位の体勢で再び腰を動かし始めた…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が後背位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に見せつけるように%STR_BODY("尻：外見", MTAR:0)%を振りながら、もっと激しく腰を動かすように要求した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：腰：長", MTAR:0)%を掴みなおすと、後ろから何度も腰を打ち付けて%ANAME(MTAR:0)%の膣奥を突いた…
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は自ら前に倒れて四つん這いになると、%ANAME(MPLY:0)%に後背位で腰を振らせた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は体をひねって自ら四つん這いになると、今度は%ANAME(MPLY:0)%に後背位で腰を振らせた…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が後背位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に後背位で挿入している%ANAME(MPLY:0)%に対し、もっと激しく腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は言われるまま腰の動きを激しくし、後ろから何度も%ANAME(MTAR:0)%の膣奥を突いた…
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を後背位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押し倒して後ろから覆い被さると、再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を後背位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を返して四つん這いにすると、再び腰を動かし始めた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は張り型を後ろから%ANAME(MTAR:0)%の%LOCALS:0%に突き入れ、何度も腰を打ち付け%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に%STR_BODY("尻：外見", MTAR:0)%を向け、後ろから%LOCALS:0%に張り型を挿入させた…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、後背位で%ANAME(MTAR:0)%を犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を四つん這いにすると、装着した張り型を%ANAME(MTAR:0)%の%LOCALS:0%に突き入れて腰を振った…
		ENDSELECT
	ENDIF

;ペニスバンド不使用
ELSE
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が後背位
				IF TFLAG:17 == 1
					SELECTCASE IFRAND("0, 2", 1, "3", ABL:(MPLY:0):主導度Ｕ >= 500)
						CASE 0
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いにした/四つん這いになった", 1)%%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：長", MTAR:0)%を%SPLIT_RAND("掴むと/鷲掴みにすると/揉みしだきながら/掴み直すと", 1, "抱え込むと", TALENT:(MTAR:0):豊尻)%、
						CASE 1
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いにした/四つん這いになった/挿入したまま", 1)%%ANAME(MTAR:0)%の%STR_BODY("腰", MTAR:0)%を%SPLIT_RAND("掴むと/掴み直すと/引き寄せると", 1)%、
						CASE 2
							PRINTFORM %ANAME(MPLY:0)%は%SPLIT_RAND("四つん這いで繋がったままの/後背位で繋がったままの/尻を突き出した姿勢の", 1)%%ANAME(MTAR:0)%に背中から覆い被さると、
						CASE 3
							PRINTFORM %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：長", MTAR:0)%を%SPLIT_RAND("割り開いて/押し広げて/掴んで", 1)%%SPLIT_RAND("尻穴を/アナルを/結合部を/繋がった部分を", 1)%%SPLIT_RAND("剥き出しにしながら/眺めながら/観察しながら/丸見えにしながら", 1)%、
					ENDSELECT
					SELECTCASE IFRAND("0, 1", 1, "2", ABL:(MPLY:0):性交 >= ランク閾値:ランク_その他:ランク_C)
						CASE 0
							PRINTFORMW さらに激しく腰を動かして膣奥を突い%TARGET_STR("た", "てきた")%…
						CASE 1
							PRINTFORMW 音を立てて激しく腰を打ち付け%TARGET_STR("た", "てきた")%…
						CASE 2
							PRINTFORMW ペニスで弱い部分を激しく擦り上げ%TARGET_STR("た", "てきた")%…
					ENDSELECT
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押し倒して後ろから覆い被さると、後背位の体勢で再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を返して四つん這いにすると、後背位の体勢で再び腰を動かし始めた…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が後背位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に見せつけるように%STR_BODY("尻：外見", MTAR:0)%を振りながら、もっと激しく腰を動かすように要求した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%STR_BODY("尻：尻肉：腰：長", MTAR:0)%を掴みなおすと、後ろから何度も腰を打ち付けて%ANAME(MTAR:0)%の膣奥を突いた…
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は自ら前に倒れて四つん這いになると、%ANAME(MPLY:0)%に後背位で腰を振らせた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は体をひねって自ら四つん這いになると、今度は%ANAME(MPLY:0)%に後背位で腰を振らせた…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が後背位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に後背位で挿入している%ANAME(MPLY:0)%に対し、もっと激しく腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は言われるまま腰の動きを激しくし、後ろから何度も%ANAME(MTAR:0)%の膣奥を突いた…
				;背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を後背位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押し倒して後ろから覆い被さると、再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を後背位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を返して四つん這いにすると、再び腰を動かし始めた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は後ろから%ANAME(MTAR:0)%の%LOCALS:0%にペニスを突き入れ、何度も腰を打ち付け%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に%STR_BODY("尻：外見", MTAR:0)%を向け、後ろから%LOCALS:0%にペニスを挿入させた…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、後背位で%ANAME(MTAR:0)%を犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を四つん這いにすると、%ANAME(MTAR:0)%の%LOCALS:0%にペニスを突き入れて腰を振った…
		ENDSELECT
	ENDIF
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST31
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

IF NOWEX:(MTAR:0):Ｖ絶頂 >= 8
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %ANAME(MTAR:0)%は支えきれなくなった上体を投げ出し、全身をビクビクと震わせている…
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%は激しい絶頂の余韻を味わいながら、%BAR_NAME(MPLY:0)%を咥え込んだ尻を淫らにくねらせている…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN31
RETURN コマンド_ウフフ
