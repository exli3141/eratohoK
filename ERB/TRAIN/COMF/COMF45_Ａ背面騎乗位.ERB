;Ａ背面騎乗位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME45
IF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && IS_INSERT_MUTUAL(MPLY, MTAR:0) != 1
	LOCALS:0 = 二穴Ａ背面騎乗位
ELSE
	LOCALS:0 = Ａ背面騎乗位
ENDIF
RESULTS:0 = %LOCALS:0%で奉仕させる
RESULTS:1 = %LOCALS:0%で責められる
RESULTS:2 = %LOCALS:0%で跨る
RESULTS:3 = %LOCALS:0%で奉仕させられる
RESULTS:4 = %LOCALS:0%で突き上げさせる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE45
;共通部分
CALL COM_ABLE_COMMON(45)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;実行者に竿がなく、ペニスバンド装着中でもないと不可
SIF !HAS_PENIS(MPLY:0) && !IS_EQUIP_PLAYER((MPLY:0), 50)
	RETURN 0
;プレイヤーとターゲットの挿入関係を取得
LOCAL:0 = IS_INSERT_MUTUAL(MPLY:0, MTAR:0)
;ターゲットからプレイヤーに挿入中なら不可
SIF LOCAL:0 == 3 || LOCAL:0 == 4
	RETURN 0
;ターゲットのＶが他のキャラの竿で埋まっていて、体位が正常位・対面座位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && LOCAL:0 != 1 && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 1, 3)
	RETURN 0
;ターゲットのＡがプレイヤーの竿以外で埋まっているなら不可
SIF IS_A_HOLD(MTAR:0) && LOCAL:0 != 2
	RETURN 0
;プレイヤーが顔面騎乗中なら不可
SIF IS_RIDE(MPLY:0)
	RETURN 0
;ターゲットが顔面騎乗されているなら不可
SIF IS_RIDDEN(MTAR:0)
	RETURN 0
;ターゲットが顔面騎乗中なら不可
SIF IS_RIDE(MTAR:0)
	RETURN 0
;プレイヤーが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;ターゲットが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;ターゲットが拘束中なら不可
SIF IS_BIND(MTAR:0)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 21) || IS_EQUIP_TARGET(MPLY:0, 21) || IS_EQUIP_PLAYER(MPLY:0, 22) || IS_EQUIP_TARGET(MPLY:0, 22)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 21) || IS_EQUIP_TARGET(MTAR:0, 21) || IS_EQUIP_PLAYER(MTAR:0, 22) || IS_EQUIP_TARGET(MTAR:0, 22)
	RETURN 0
;ターゲットがプレイヤー／ターゲットがプレイヤーに乳首吸い中なら不可
SIF SEARCH_EQUIP_IC(9, MTAR:0, MPLY:0) >= 0
	RETURN 0
;ターゲットがプレイヤーにＷ手コキ、Ｔ手コキ中なら不可
LOCAL:0 = SEARCH_EQUIP(10, MTAR:0, MPLY:0)
SIF LOCAL:0 >= 0 && MEQUIP_PLAYER_NUM:(LOCAL:0) >= 2
	RETURN 0
;プレイヤーが素股・足コキ・尻コキ・イラマ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 13, 14, 15, 90, 103)
	RETURN 0
;プレイヤーがフェラ・パイズリ・素股・足コキ・尻コキ・オナホコキ・電気按摩・触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MPLY:0, 11, 12, 13, 14, 15, 16, 103, 107, 205)
	RETURN 0
;プレイヤーがセルフオナホ中ならダメ
SIF IS_EQUIP_PLAYER(MPLY:0, 116)
	RETURN 0
;ターゲットがクンニされているなら不可
SIF IS_EQUIP_TARGET(MTAR:0, 2)
	RETURN 0
;ターゲットからプレイヤーにイラマ中なら不可
SIF SEARCH_EQUIP(90, MTAR:0, MPLY:0) >= 0
	RETURN 0
;プレイヤーからターゲットに足舐め中なら不可
SIF SEARCH_EQUIP(104, MPLY:0, MTAR:0) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM45
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 2
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 1
		CASE 2
			TFLAG:17 = 1
		CASE 3
			TFLAG:17 = 2
		CASE 4
			TFLAG:17 = 2
		CASE 5
			TFLAG:17 = 3
		CASE 6
			TFLAG:17 = 4
		CASE 7
			TFLAG:17 = 1
		CASE 8
			TFLAG:17 = 1
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 40
			TFLAG:17 = 1
		CASE 41
			TFLAG:17 = 1
		CASE 42
			TFLAG:17 = 2
		CASE 43
			TFLAG:17 = 2
		CASE 44
			TFLAG:17 = 3
		CASE 45
			TFLAG:17 = 4
		CASE 47
			TFLAG:17 = 1
		CASE 48
			TFLAG:17 = 1
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_TARGET_P(MTAR:0, MPLY:0, 1500)
SOURCE:(MPLY:0):露出 = 200
SOURCE:(MPLY:0):性行動 = 240

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 80)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_ASEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 180, 150)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 180

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ａ = SENSE_SEX_PLAYER(MTAR:0, MPLY:0, 1500)
SOURCE:(MTAR:0):性行動 = 240

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 80)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_ASEX_A(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 200, 150)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:(MTAR:0):3 |= STAIN:(MPLY:0):2
STAIN:(MPLY:0):2 |= STAIN:(MTAR:0):3

;射精箇所と対象をセット(ターゲット側)
CALL STACK_SPERM(MTAR:0, MPLY:0, 0)

;体位フラグのセット
TCVAR:(MPLY:0):31 = 6
TCVAR:(MTAR:0):35 = 6

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 5

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP45
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP45(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:2, 750)
SOURCE:(LOCAL:2):露出 += 200
SOURCE:(LOCAL:2):性行動 += 80

;Ａ性交の共通処理
CALL COM_COMMON_ASEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 4

DOWNBASE:(LOCAL:3):体力 += 30

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ａ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, 750)
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 80

;Ａ性交の共通処理
CALL COM_COMMON_ASEX_A(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 4

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE45(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のアナルに背面騎乗位で挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP45(ARG:0)
	IF ABL:(MEQUIP_TARGET:(ARG:0):0):Ａ感 >= 3
		LOCALS:0 = 十分に開発されたアナル
	ELSEIF ABL:(MEQUIP_TARGET:(ARG:0):0):Ａ感 >= 2
		LOCALS:0 = 開発途上のアナル
	ELSE
		LOCALS:0 = キツく閉じたアナル
	ENDIF

	LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
	SELECTCASE RAND:2
		CASE 0
			;ターゲットが行動不能
			IF !IS_PLAYABLE(LOCAL:0)
				PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が下から%EQUIP_TARGET_ANAME(ARG:0)%のアナルを突き上げている…
			ELSE
				PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%が%EQUIP_PLAYER_ANAME(ARG:0)%に背を向けて跨がり、尻穴で%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥えこんでいる…
			ENDIF
		CASE 1
			;ターゲットが行動不能
			IF !IS_PLAYABLE(LOCAL:0)
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は気絶した%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%を下から激しく突き上げている…
					CASE 1
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%はぐったりとした%ANAME((MEQUIP_TARGET:(ARG:0):0))%の%LOCALS:0%を後ろ向きで腰の上に乗せたまま、下から突き上げている…
				ENDSELECT
			ELSE
				SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
					CASE 0
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は背面騎乗位の体勢で%LOCALS:0%に挿入したままの%EQUIP_TARGET_ANAME(ARG:0)%に尻を振らせている…
							CASE 1
								PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の尻の動きに合わせて腰を突き上げ、%EQUIP_PLAYER_ANAME(ARG:0)%のソレを%LOCALS:0%の奥深くまで押し込んでいる…
						ENDSELECT
					;ターゲットに主導権
					CASE 1
						PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%のソレを%LOCALS:0%で締め付けながら、さらに尻を振って%EQUIP_PLAYER_ANAME(ARG:0)%を責め立てている…
					;第三者に主導権
					CASEELSE
						PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_PLAYER_ANAME(ARG:0)%に背を向けて跨る%EQUIP_TARGET_ANAME(ARG:0)%に対して、もっと激しく尻を振るように命令し
						PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%のソレを%LOCALS:0%で締め付けながら、命じられるままにひたすら腰を振っている…
				ENDSELECT
			ENDIF
	ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP45(ARG:0)
LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
IF IS_BIND(LOCAL:0) || !IS_PLAYABLE(LOCAL:0)
	PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の体を持ち上げて尻穴から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…
ELSE
	PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%は腰を上げて%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を尻穴から引き抜いた…
ENDIF

;-------------------------------------------------
;固有の実行判定(ターゲット側)
;-------------------------------------------------
@COM_ORDER_TARGET45(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120

;アナルセックス共通
CALL COM_ORDER_ASEX_A(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE45
IF ABL:(MTAR:0):Ａ感 >= 3
	LOCALS:0 = 十分に開発されたアナル
ELSEIF ABL:(MTAR:0):Ａ感 >= 2
	LOCALS:0 = 開発途上のアナル
ELSE
	LOCALS:0 = キツく閉じたアナル
ENDIF
IF TALENT:(MTAR:0):美尻
	LOCALS:1 = %STR_BODY("尻：尻肉：長：愛撫：感触", MTAR:0)%を割り開き、
ELSEIF TALENT:(MTAR:0):豊尻
	LOCALS:1 = %STR_BODY("尻肉：長", MTAR:0)%をかきわけ、
ELSE
	LOCALS:1 = 
ENDIF
IF TALENT:(MTAR:0):美尻
	LOCALS:2 = 尻肉を割り開き、
ELSEIF TALENT:(MTAR:0):豊尻
	LOCALS:2 = 尻肉をかきわけ、
ELSE
	LOCALS:2 = 
ENDIF
;ペニスバンド装着中
IF IS_EQUIP_PLAYER((MPLY:0), 50)
	;ターゲットが行動不能
	IF !IS_PLAYABLE(MTAR:0)
		;第三者に主導権
		IF !GROUPMATCH(GET_COM_INITIATIVE(), 0, 1)
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の尻穴を背面騎乗位の体勢で犯すように命令した
		ENDIF
		PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%を後ろ向きにして腰の上に乗せ、%LOCALS:1%下から張り型で%ANAME(MTAR:0)%の%LOCALS:0%を突き上げた…

	;既に挿入済みの場合
	ELSEIF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きで上に跨がらせ、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振らせた…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%に後ろを向かせ、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振らせた…
				;前回が背面騎乗位
				ELSE
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の尻の動きに合わせて腰を突き上げ、%LOCALS:1%張り型をアナルの奥深くまで押し込ん%TARGET_STR("だ", "できた")%…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%の上に跨がり、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振り始めた…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(MTAR:0)%は挿入したまま後ろ向きになると、%LOCALS:2%今度は背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振り始めた…
				;前回が背面騎乗位
				ELSE
					PRINTFORML %ANAME(MTAR:0)%は尻穴で%ANAME(MPLY:0)%の張り型を締め付けながら、%LOCALS:2%さらに%STR_BODY("尻：長", MTAR:0)%を振って快楽を貪っている…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対し、%ANAME(MPLY:0)%の上になって背面騎乗位で腰を振るように命令した
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に背を向けて跨ると、%LOCALS:1%言われるまま尻を振ってアナルに挿入された張り型を出し入れした…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に跨って腰を振る%ANAME(MTAR:0)%に対し、後ろを向いて背面騎乗位の体勢になるように命令した
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%に背を向けると、%LOCALS:2%再び張り型を挿入された%STR_BODY("尻：長", MTAR:0)%を振り始めた…
				;前回が背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に背を向けて跨る%ANAME(MTAR:0)%に対して、もっと激しく尻を振るように命令した
					PRINTFORMW %ANAME(MTAR:0)%は尻穴で%ANAME(MPLY:0)%の張り型を締め付けながら、%LOCALS:2%命じられるままにひたすら%STR_BODY("尻：長", MTAR:0)%を上下に振りたてた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、後ろ向きに跨って尻穴で張り型を咥え込むように要求した
				PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%に尻を向け、%LOCALS:1%反り立つ張り型を自身の%LOCALS:0%へと挿入した…
			;ターゲットに主導権
			CASE 1
				PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に尻を向けて跨がると、%LOCALS:1%張り型を自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、後ろ向きに%ANAME(MPLY:0)%に跨って、尻穴で張り型を咥え込むように命令した
				PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の上に背を向けて跨り、%LOCALS:1%反り立つ張り型を自身の%LOCALS:0%へと挿入した…
		ENDSELECT

		IF ABL:(MTAR:0):性技 >= 3 && MTAR:0 != MASTER
			PRINTFORM %ANAME(MTAR:0)%は
			IF TALENT:(MTAR:0):小柄体型
				PRINTFORML %ANAME(MPLY:0)%の腰の上で%STR_BODY("尻：長", MTAR:0)%を上下に振り動かしている…
			ELSE
				PRINTFORML 自ら%ANAME(MPLY:0)%の腰に%STR_BODY("尻：長", MTAR:0)%を打ち付けている…
			ENDIF
		ENDIF
	ENDIF

;ペニスバンド不使用
ELSE
	;ターゲットが行動不能
	IF !IS_PLAYABLE(MTAR:0)
		;第三者に主導権
		IF !GROUPMATCH(GET_COM_INITIATIVE(), 0, 1)
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の尻穴を背面騎乗位の体勢で犯すように命令した
		ENDIF
		PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%を後ろ向きにして腰の上に乗せ、%LOCALS:1%下から%ANAME(MTAR:0)%の%LOCALS:0%を激しく突き上げた…

	;既に挿入済みの場合
	ELSEIF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きで上に跨がらせ、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振らせた…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%に後ろを向かせ、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振らせた…
				;前回が背面騎乗位
				ELSE
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の尻の動きに合わせて腰を突き上げ、%LOCALS:1%ペニスをアナルの奥深くまで押し込ん%TARGET_STR("だ", "できた")%…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%の上に跨がり、%LOCALS:2%背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振り始めた…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(MTAR:0)%は挿入したまま後ろ向きになると、%LOCALS:2%今度は背面騎乗位の体勢で%STR_BODY("尻：長", MTAR:0)%を振り始めた…
				;前回が背面騎乗位
				ELSE
					PRINTFORML %ANAME(MTAR:0)%は尻穴を締めながら、%LOCALS:2%さらに激しく%STR_BODY("尻：長", MTAR:0)%を振って%ANAME(MPLY:0)%のペニスを擦り上げ%PLAYER_STR("た", "てきた")%…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が正常位・後背位・対面座位・背面座位
				IF GROUPMATCH(TFLAG:17, 1, 2)
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対し、%ANAME(MPLY:0)%の上になって背面騎乗位で腰を振るように命令した
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に背を向けて跨ると、%LOCALS:1%言われるまま尻を振ってアナルに挿入されたペニスを擦り上げた…
				;前回が騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に跨って腰を振る%ANAME(MTAR:0)%に対し、後ろを向いて背面騎乗位の体勢になるように命令した
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%に背を向けると、%LOCALS:1%再び尻を振ってペニスを擦り上げた…
				;前回が背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に背を向けて跨る%ANAME(MTAR:0)%に対して、もっと激しく尻を振るように命令した
					PRINTFORMW %ANAME(MTAR:0)%は尻穴で%ANAME(MPLY:0)%のペニスを締め付けながら、%LOCALS:2%命じられるままにひたすら%STR_BODY("尻：長", MTAR:0)%を上下に振りたてた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、後ろ向きに跨って尻穴でペニスを咥え込むように要求した
				PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%に尻を向け、%LOCALS:1%反り立つペニスを自身の%LOCALS:0%へと挿入した…
			;ターゲットに主導権
			CASE 1
				PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に尻を向けて跨がると、%LOCALS:1%ペニスを自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、後ろ向きに%ANAME(MPLY:0)%に跨って、尻穴でペニスを咥え込むように命令した
				PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の上に背を向けて跨り、%LOCALS:1%反り立つペニスを自身の%LOCALS:0%へと挿入した…
		ENDSELECT

		IF ABL:(MTAR:0):性技 >= 3 && MTAR:0 != MASTER
			PRINTFORM %ANAME(MTAR:0)%は
			IF TALENT:(MTAR:0):小柄体型
				PRINTFORML %ANAME(MPLY:0)%の腰の上で%STR_BODY("尻：長", MTAR:0)%を上下に振り動かしている…
			ELSE
				PRINTFORML 自ら%ANAME(MPLY:0)%の腰に%STR_BODY("尻：長", MTAR:0)%を打ち付けている…
			ENDIF
		ENDIF
	ENDIF
ENDIF
WAIT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST45
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%のアヌスをごりごりと抉っていく……

IF MPLY:0 != MASTER
ENDIF

IF MTAR:0 != MASTER
	LOCAL:2 = CUP:(MTAR:0):快Ａ

	IF TCVAR:(MTAR:0):52
		;気絶中
		PRINTFORML %ANAME(MTAR:0)%は尻穴を突き上げられるたびにガクガクと体を震わせ、口端からだらしなく涎を垂らしている
	ELSEIF LOCAL:2 < 300 
		PRINTFORML %ANAME(MTAR:0)%は必死に腰を動かすが、まだ快楽を得るには遠いようで、少し苦しそうだ
	ELSEIF LOCAL:2 < 1000
		PRINTFORML %ANAME(MTAR:0)%は苦しそうにしているが、その声には快楽の喘ぎが混じっている
	ELSEIF LOCAL:2 < 3000
		PRINTFORML %ANAME(MTAR:0)%は腰を振りながらこらえきれない快楽に可愛らしい声を漏らしている
	ELSEIF LOCAL:2 < 6000
		PRINTFORML %ANAME(MTAR:0)%は大分慣れてきたのか、快楽に突き上げられながら自ら進んで腰を振っている
	ELSEIF LOCAL:2 < 10000
		PRINTFORM %ANAME(MTAR:0)%は
		IF TALENT:(MTAR:0):抑圧 || TALENT:(MTAR:0):孤高 || TALENT:(MTAR:0):快感の否定
			PRINTFORML 快楽を否定する言葉を吐きつつ、こらえ切れない喘ぎ声を上げ続けている
		ELSE
			IF TALENT:(MTAR:0):小柄体型
				PRINTFORM その小さな身体で
			ENDIF
			PRINTFORML 一生懸命腰を振り背中を反らしながら快楽の喘ぎ声を上げた
		ENDIF
	ELSE
		PRINTFORML %ANAME(MTAR:0)%はあられもない嬌声を上げながら、激しく腰を振り快楽を貪っている
	ENDIF
ENDIF
SIF TALENT:(MTAR:0):豊尻
	PRINTFORML %ANAME(MTAR:0)%の豊かな尻肉は、抽送のたびにふるふると震えている……

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN45
RETURN コマンド_ウフフ
