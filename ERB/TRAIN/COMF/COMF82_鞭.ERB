;鞭

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME82
LOCALS:0 = 鞭

RESULTS:0 = %LOCALS:0%で打つ
RESULTS:1 = %LOCALS:0%で打たされる
RESULTS:2 = %LOCALS:0%で打たせる
RESULTS:3 = %LOCALS:0%で打たれる
RESULTS:4 = %LOCALS:0%で打たせる
RESULTS:5 = %LOCALS:0%打ち見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE82
;共通部分
CALL COM_ABLE_COMMON(82)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;鞭が必要
SIF !ITEM:鞭 && !ITEM:A_鞭
	RETURN 0
;プレイヤーがＶかＡに挿入されているなら不可
SIF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーがキス中なら不可
SIF IS_EQUIP(MPLY:0, 20)
	RETURN 0
;プレイヤーが手コキ・フェラ・パイズリ・素股・尻コキ中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 10, 11, 12, 13, 15)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;顔面騎乗されているなら1人のみ
	SIF IS_RIDDEN(MTAR:(LOCAL:0)) && MTAR_NUM >= 2
		RETURN 0
	;キス中なら不可
	SIF IS_EQUIP(MTAR:(LOCAL:0), 20)
		RETURN 0
	;プレイヤーからターゲット／ターゲットからプレイヤーに胸愛撫中なら不可
	SIF SEARCH_EQUIP_IC(1, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
	SIF SEARCH_EQUIP_IC(104, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM82
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 100
EXP:(MPLY:0):嗜虐経験値 += RAND:3 + 1

SOURCE:(MPLY:0):嗜虐 = 400
SOURCE:(MPLY:0):逸脱 = 150

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 50)

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):苦痛 = 400 + MIN(ABL:(MPLY:0):武闘, 90) * 3
	SOURCE:(LOCAL:1):露出 = 50
	SOURCE:(LOCAL:1):逸脱 = 150

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 0, 170)
	EXP:(LOCAL:1):被虐経験値 += RAND:3 + 1

	IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
		TIMES SOURCE:(LOCAL:1):苦痛, 1.50
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER82(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"サドLv{ABL:(ARG:0):サド}", ABL:(ARG:0):サド * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE82
;ターゲットが２人
IF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	LOCALS:1 = 交互に

;ターゲットが１人
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:1 = 
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF MPLY:0 == MASTER
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%を%LOCALS:1%鞭で何度も打ち据えた…
		ELSEIF ABL:(MPLY:0):サド >= 3
			PRINTFORMW %ANAME(MPLY:0)%は嗜虐的な表情を浮かべると、%LOCALS:0%を鞭で%LOCALS:1%激しく打ち据えた…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%はやや緊張するように鞭を構えると、%LOCALS:0%の体に振り下ろした…
		ENDIF
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:0%は%ANAME(MPLY:0)%に鞭を持たせると、それで叩くように命令した
		IF MPLY:0 == MASTER
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に言われるまま、%LOCALS:0%を%LOCALS:1%何度も鞭で打った…
		ELSEIF ABL:(MPLY:0):サド >= 3
			PRINTFORMW %ANAME(MPLY:0)%は呆れたように苦笑すると、遠慮無く%LOCALS:0%の体に%LOCALS:1%鞭を振り下ろし%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は困惑しながらも、言われた通りに%LOCALS:0%の体に%LOCALS:1%鞭を振るった…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:0%の体を鞭で叩くように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の体に鞭を%LOCALS:1%振り下ろした…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST82
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):怖主
		LOCAL:3 = CUP:(LOCAL:1):怒主

		IF ABL:(LOCAL:1):マゾ >= 5
			PRINTFORML %ANAME(LOCAL:1)%は痛みを感じているにもかかわらず嬉しそうに甘い声を上げている
		ELSEIF ABL:(LOCAL:1):マゾ >= 3
			PRINTFORML %ANAME(LOCAL:1)%は悲鳴を上げるが、その声には甘い色が混じっている
		ELSEIF LOCAL:2 >= 3000
			PRINTFORML %ANAME(LOCAL:1)%はあまりの痛みと恐怖に恥も外聞もなく泣き叫んでいる
		ELSEIF LOCAL:2 >= 1500
			PRINTFORML %ANAME(LOCAL:1)%は痛みに耐えきれず、何度も悲鳴を上げながら悶え狂っている
		ELSEIF LOCAL:2 >= 500
			PRINTFORML %ANAME(LOCAL:1)%は痛みのあまり口から呻きを漏らしている
		ELSE
			IF LOCAL:3 >= 200 && !IS_INITIATIVE(MTAR:(LOCAL:0))
				PRINTFORML %ANAME(LOCAL:1)%は痛みに耐えながら、%ANAME(MPLY:0)%を睨みつけた
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%は歯を食いしばって痛みに耐えている
			ENDIF
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN82
RETURN コマンド_ウフフ
