;酒池肉林

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME51
LOCALS:0 = 酒恥肉倫
RESULTS:0 = %LOCALS:0%
RESULTS:1 = %LOCALS:0%
RESULTS:2 = %LOCALS:0%
RESULTS:3 = %LOCALS:0%
RESULTS:4 = %LOCALS:0%
RESULTS:5 = %LOCALS:0%

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE51
;共通部分
CALL COM_ABLE_COMMON(51)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人, MASTER
SIF MPLY:0 != MASTER || MPLY_NUM != 1
	RETURN 0

;ターゲット1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;露出プレイ中でないと駄目
SIF TFLAG:54 !=  8
	RETURN 0

;男達に見つかっていないと駄目
SIF TFLAG:103 != 2
	RETURN 0

;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;口枷装着中なら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 86)
		RETURN 0
	;キス中なら不可
	SIF IS_EQUIP(MPLY:(LOCAL:0), 20)
		RETURN 0
	;フェラ・パイズリ・亀頭フェラ中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 107)
		RETURN 0
	;イラマ・触手口辱されているなら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 90, 210)
		RETURN 0
	;拘束されているなら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MTAR:(LOCAL:0))
		RETURN 0
	;挿入中なら不可
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "全")
		RETURN 0
	;キス中なら不可
	SIF IS_EQUIP(MTAR:(LOCAL:0), 20)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM51
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;選択肢の表示の都合上、ここに書かないとどうにも
LOCALS:0 = %ANAME(MPLY:0)%
;ターゲットが２人
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
;ターゲットが１人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

PRINTFORML %LOCALS:1%を男達に抱かせてやることにした……
PRINTFORMW 痴態に興奮しきっていた男達は、我先にと%LOCALS:1%に群がっていく……
IF IS_FEMALE(MASTER)
	PRINTFORML 自分も混ざりますか？
	CALL ASK_YN
	IF RESULT == 0
		TFLAG:16 = 1
	ENDIF
ENDIF

;プレイヤー（MASTER)が女で、選択肢に「混ざる」を選択した場合
IF TFLAG:16
	CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("行きずりの男"), @"行きずりの男の\@ RAND:2 ? ペニス # 唇\@", "行きずりの男")
ENDIF
FOR LOCAL, 0, MTAR_NUM
	CALL FUCK_GANGBANG(MTAR:LOCAL, GET_SPERM_ID("行きずりの男"), @"行きずりの男の\@ RAND:2 ? ペニス # 唇\@", "行きずりの男")
NEXT

;主導度変化基準値
TFLAG:49 = 5

;倒錯度変化基準値
TFLAG:50 = 10

TFLAG:55 = TFLAG:56

RETURN 1


;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER51(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE51
IF TFLAG:16
	LOCALS:0 = %ANAME(MPLY:0)%や
ELSE
	LOCALS:0 = 
ENDIF
;ターゲットが２人
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
;ターゲットが１人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF
PRINTFORMW 男達は代わる代わる、%LOCALS:0%%LOCALS:1%を組み敷いては、その穴に種を植え付けていく……
PRINTFORMW 宴は深夜になるまで続いた
;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN51
RETURN コマンド_ウフフ
