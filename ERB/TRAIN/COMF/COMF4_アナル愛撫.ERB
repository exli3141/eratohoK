;アナル愛撫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME4
IF IS_EQUIP_TARGET(MTAR:0, 62)
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時Ａバイブ責め
	ELSE
		LOCALS:0 = Ａバイブ責め
	ENDIF
ELSE
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時アナル愛撫
	ELSE
		LOCALS:0 = アナル愛撫
	ENDIF
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE4
;共通部分
CALL COM_ABLE_COMMON(4)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;プレイヤー行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤー土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが足コキ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;ターゲットのＡがプレイヤーの指またはアナルバイブ以外で埋まっているなら不可
	SIF IS_A_HOLD(MTAR:(LOCAL:0)) && SEARCH_EQUIP(4, MPLY:0, MTAR:(LOCAL:0)) == -1 && !IS_EQUIP_TARGET(MTAR:(LOCAL:0), 62)
		RETURN 0
	;ターゲットが顔面騎乗中なら不可
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;ターゲットからプレイヤーに挿入中で体位が後背位・背面座位なら不可
	SIF GROUPMATCH(GET_SEX_POSITION(MTAR:(LOCAL:0), MPLY:0), 2, 4)
		RETURN 0
NEXT
;ターゲットのうち片方だけがバイブ挿入中なら不可
SIF MTAR_NUM >= 2 && IS_EQUIP_TARGET(MTAR:0, 62) != IS_EQUIP_TARGET(MTAR:1, 62)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM4
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
	CASE 4
		TIMES LOCAL:10, 0.50
	CASE 5
		TIMES LOCAL:10, 0.45
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 60
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 120, 60)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

		SOURCE:(LOCAL:3):快Ａ += LOCAL:4

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0)
	NEXT
NEXT

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):露出 = 120
	SOURCE:(LOCAL:1):接触 = 60
	SOURCE:(LOCAL:1):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 70, 100)

	;射精箇所と対象をセット
	FOR LOCAL:2, 0, MPLY_NUM
		LOCAL:3 = MPLY:(LOCAL:2)
		CALL STACK_SPERM(LOCAL:3, LOCAL:1, 0)
	NEXT
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP4
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP4(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0, 0)
	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 2
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 40
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;射精箇所と対象をセット
	FOR LOCAL:1, 0, MEQUIP_PLAYER_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(LOCAL:1)
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0, 0)
	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 2
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE4(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 62)
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%をＡバイブ責め中
ELSE
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のアナルに指を挿入中
ENDIF

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP4(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 62)
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のアナルに挿入されたバイブを前後させている…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のアナルを指でかき回している…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP4(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER4(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE4
;ターゲットが２人のとき
IF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
;ターゲットが１人のとき
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
ENDIF

IF TALENT:(MTAR:0):美尻
	LOCALS:1 = %STR_BODY("尻：尻肉：長：愛撫：感触", MTAR:0)%を割り開き、
ELSEIF TALENT:(MTAR:0):豊尻
	LOCALS:1 = %STR_BODY("尻肉：長", MTAR:0)%をかきわけ、
ELSE
	LOCALS:1 = 
ENDIF


SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%の%LOCALS:1%アナルで振動するバイブを抜き差しして責め立て%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%の%LOCALS:1%アナルに指を挿れて中をかき回し%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORMW %LOCALS:0%は%LOCALS:1%自身のアナルで振動するバイブを%ANAME(MPLY:0)%に握らせ、抜き差しさせた…
		ELSE
			PRINTFORMW %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の指を自身のアナルに挿入させ、中を愛撫させた…
		ENDIF
	;第三者に主導権
	CASEELSE
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:0%のアナルに挿れられたバイブを動かすよう命令した
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%の%LOCALS:1%尻穴で振動するバイブを抜き差しして責め立てた…
		ELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:0%のアナルに指を入れて愛撫するよう命令した
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%の%LOCALS:1%尻穴に指を挿れて中を弄り回した…
		ENDIF
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST4
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):快Ａ

		IF TCVAR:(LOCAL:1):52
			;気絶中
			PRINTFORML アナルへの刺激に、%ANAME(LOCAL:1)%は意識を失ったまま小さく呻いた
		ELSEIF LOCAL:2 < 300 
			PRINTFORML %ANAME(LOCAL:1)%の開発されていないそこはまだ異物を受け入れる場所ではなく、%ANAME(LOCAL:1)%は痛そうに顔を歪めた
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%は苦しそうにしながらもその表情にはわずかな快楽と戸惑いが見える
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%はこらえきれない快楽に恥ずかしそうな声を漏らしている
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%は大分こなれてきた肛門の感覚に、時折自ら快楽を求めるように小さく腰を揺らしている
		ELSEIF LOCAL:2 < 10000
			PRINTFORM %ANAME(LOCAL:1)%は
			IF TALENT:(LOCAL:1):抑圧 || TALENT:(LOCAL:1):孤高 || TALENT:(LOCAL:1):快感の否定
				PRINTFORML あらがえない快楽に、涙目になりながら喘ぎ声を上げている
			ELSE
				PRINTFORML 翻弄する指の動きに合わせて背中を反らし、直腸の刺激に快楽の喘ぎ声を上げた
			ENDIF
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%は淫らに腰を揺らしながら、すっかり菊穴の快楽に溺れて嬌声を上げている
		ENDIF
		IF TALENT:(LOCAL:1):美尻
			PRINTFORML %STR_BODY("尻肉：短：外見", MTAR:0)%がふるふると震えている……
		ELSEIF TALENT:(LOCAL:1):豊尻
			PRINTFORML %STR_BODY("尻肉：短：外見", MTAR:0)%がふるふると震えている……
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN4
RETURN コマンド_ウフフ
