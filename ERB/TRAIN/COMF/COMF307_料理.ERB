;料理

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME307
RESULTS:0 = 料理に付き合わせる
RESULTS:1 = 料理に付き合わされる

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE307
;共通部分
CALL COM_ABLE_COMMON(307)
SIF RESULT == 0
	RETURN 0
;捕虜会話では不可
SIF FLAG:調教モード == 6
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;デート中は無理
SIF TFLAG:54
	RETURN 0
;臨月or怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月 || CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM307
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "料理"), 0)

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE307
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と一緒に料理を作った
ELSE
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と一緒に料理を作ることになった
ENDIF

IF ABL:(MPLY:0):料理 < 0 || ABL:(MTAR:0):料理 < 0
	IF TFLAG:18 == 1
		PRINTFORML どうにか人間でも食べられる物に仕上がった…
	ELSEIF TFLAG:18 == -1
		IF RAND:2 == 0
			PRINTFORML ……臭いだけで気絶しそうなシロモノが完成した
		ELSE
			PRINTFORML ……皿の中に何やら怪しく蠢いているものが見える
		ENDIF
	ELSE
		PRINTFORML 精神修行だと思えば食べられなくもない……と思う
	ENDIF
ELSE
	IF TFLAG:18 == 1
		PRINTFORML 思った以上に美味しく仕上がった…
	ELSEIF TFLAG:18 == -1
		PRINTFORML ……見事な失敗作が出来上がった
	ENDIF
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST307
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN307
RETURN コマンド_日常
