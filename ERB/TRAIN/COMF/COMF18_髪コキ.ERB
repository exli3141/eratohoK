;オナホコキ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME18
IF MPLY_NUM == 2
	LOCALS:0 = Ｗ髪コキ
ELSE
	LOCALS:0 = 髪コキ
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE18
;共通部分
CALL COM_ABLE_COMMON(18)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0

;全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;挿入中はプレイヤーが一人の場合のみ許可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "全") && MPLY_NUM >= 2
		RETURN 0
	;ミドル以上が必要
	SIF TALENT:(MPLY:(LOCAL:0)):髪の長さ < 275
		RETURN 0
	;フェラ、パイズリ、素股、足コキ、尻コキ、尾菜穂、尻尾中は無理
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 13, 14, 15, 16, 17)
		RETURN 0
NEXT
;ターゲットに竿が必要
SIF !HAS_PENIS(MTAR:0)
	RETURN 0
;ターゲットの竿が挿入中なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ")
	RETURN 0
;ターゲットが触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MTAR:0, 205)
	RETURN 0
;ターゲットがオナホコキしているなら不可
SIF IS_EQUIP_PLAYER(MTAR:LOCAL, 116)
	RETURN 0
SIF IS_EQUIP_TARGET(MTAR:0,, 11, 12, 13, 14, 15, 16, 17)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM18
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;対Ｐ奉仕系コマンドの継続フラグを解除
CALL RELEASE_SERVE_P

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 250)
	SOURCE:(LOCAL:2):接触 = 40
	SOURCE:(LOCAL:2):性行動 = 210

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 180, 80)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットに与える快感系ソースを計算
	SOURCE:(MTAR:0):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, MTAR:0, 500) * LOCAL:10 / 100

	;射精箇所と対象をセット
	CALL STACK_SPERM(MTAR:0, LOCAL:2, RAND:3 ? 射精部位_髪 # 射精部位_顔)
NEXT

;●ターゲットについて判定
SOURCE:(MTAR:0):接触 = 20
SOURCE:(MTAR:0):性行動 = 180

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 60)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP18
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP18(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 20
	SOURCE:(LOCAL:2):性行動 += 70

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットに与える快感系ソースを計算
	SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100

	;射精箇所と対象をセット
	CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 射精部位_髪 # 射精部位_顔)
NEXT

;●ターゲットについて判定
SOURCE:(LOCAL:3):接触 += 10
SOURCE:(LOCAL:3):性行動 += 60

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE18(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に髪コキ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP18(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のペニスに髪を絡めてシゴき上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP18(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER18(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
ENDIF

;汚れの判定
;IF !TALENT:(ARG:0):汚れ無視
;	LOCAL:0 = 0
;	;愛液の汚れ
;	SIF STAIN:PLAYER:2 & 1
;		LOCAL:0 += 1
;	;精液の汚れ
;	SIF STAIN:PLAYER:2 & 4
;		LOCAL:0 += 3
;	;アナルの汚れ
;	SIF STAIN:PLAYER:2 & 8
;		LOCAL:0 += 7
;	SIF STAIN:PLAYER:2 & 18
;		LOCAL:0 += 15
;
;	SIF TALENT:(ARG:0):汚臭鈍感
;		LOCAL:0 /= 3
;	SIF TALENT:(ARG:0):汚臭敏感
;		LOCAL:0 *= 2
;
;	;汚れの影響が小さい
;	LOCAL:0 /= 3
;
;	;汚れあり
;	IF LOCAL:0 > 0
;		CALL COM_ORDER_ELEMENT(ARG:0, "汚れあり", -LOCAL:0)
;	ENDIF
;ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE18
;プレイヤーが２人
IF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%はそれぞれ髪を%ANAME(MTAR:0)%のペニスに絡め、いやらしくシゴき上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、髪をペニスに絡ませシゴかせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%のペニスに髪を絡めてシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%はそれぞれ髪を%ANAME(MTAR:0)%のペニスに絡め、いやらしくシゴき上げた…
	ENDSELECT

;プレイヤーが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は髪を%ANAME(MTAR:0)%のペニスに絡め、いやらしくシゴき上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に、髪をペニスに絡ませシゴかせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%のペニスに髪を絡めてシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%は髪を%ANAME(MTAR:0)%のペニスに絡め、いやらしくシゴき上げた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN18
RETURN コマンド_ウフフ
