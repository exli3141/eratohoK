;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K98_DESCENDANT_OF_LILIPUT_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K98_DESCENDANT_OF_LILIPUT_DECISION(対象)
#DIM 対象
#DIM 正邪

正邪 = NAME_TO_CHARA("正邪")

SIF KDVAR:正邪:正邪_反逆の狼煙 != 1
	RETURN 0

SIF KDVAR:対象:針妙丸_小人の末裔
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 0) && CHECK_KOJO_DAILY_HAPPEN(正邪, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K98_DESCENDANT_OF_LILIPUT_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K98_DESCENDANT_OF_LILIPUT(対象)
#DIM 対象
#DIM 正邪

CFLAG:対象:面識 = 1

正邪 = NAME_TO_CHARA("正邪")

PRINTFORMW 「あっ、あなたが%ANAME(正邪)%の連れてきた人？」
PRINTFORML 拠点を巡回していると、いきなり声をかけられた
PRINTFORMW ……のだが、誰も見当たらない……
PRINTFORMW 「こっちこっち！　下だよ、下」
PRINTFORML 見ると、やけに小さな子がこちらを見上げていた
PRINTFORMW 小人というやつだろうか……
PRINTFORMW 「初めまして、私は%NAME_FORMAL(対象)%。%ANAME(正邪)%と一緒に弱者の楽園を作ろうとしてるんだ」
PRINTFORMW 「あなたも手伝ってくれるって%ANAME(正邪)%から聞いてるよ。よろしくね！」
PRINTFORML 握手を求めて手を差し出してきたので、とりあえず小指を差し出した
PRINTFORMW %ANAME(対象)%はそれを抱えるように握った。それでよかったらしい
PRINTFORMW 「%ANAME(正邪)%は気難しいから、もしかしたら不愉快な思いをすることもあるかもしれないけど」
PRINTFORMW 「でも、根はいい子だから、できれば優しい目で見てあげてね」
PRINTFORMW 身体は小さいが、まるで保護者のようだ
PRINTFORMW %ANAME(対象)%に免じて、頷いておくことにした……

KDVAR:対象:針妙丸_小人の末裔 = 1

RETURN 1
