;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K13_THINKING_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K13_THINKING_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:橙_考え事
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("藍"))
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)


;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K13_THINKING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K13_THINKING(対象)
#DIM 対象

SIF !CFLAG:対象:面識
	CFLAG:対象:面識 = 1

	KDVAR:対象:橙_考え事 = 1


PRINTFORMW 「うーん？　うーん、うーん……」
PRINTFORML 廊下を見回っていると、%ANAME(対象)%に出くわした
PRINTFORMW なにやらうなっているが……
CALL ASK_YN("声をかける", "放っておく")

IF RESULT == 1
	PRINTFORMW 気にすることでもないだろう
	PRINTFORMW 放っておくことにした
	RETURN 1
ENDIF

PRINTFORMW 「あ、%ANAME(MASTER)%」
PRINTFORMW 「実は手下を増やそうと思って、野良猫に声をかけて回ってるんだけど」
PRINTFORMW 「あいつら野良だからさ、話しても通じなくって」
PRINTFORML 「どうしようかなぁって……うーん……」
CALL ASK_MULTI("通じるまで話したらどうだ", "餌で釣るのはどうだ", "またたびなんてどうだ", "いっそ別の動物にしたらどうだ", "諦めたらどうだ")

SELECTCASE RESULT
	CASE 0
		PRINTFORML 何事もごり押しで解決するのが一番だ
		PRINTFORMW 通じるまで話したらどうだ……
		PRINTFORMW 「うーん？　そんなのでいいのかなぁ」
		PRINTFORMW 「まあ、やってみるね」
		PRINTFORMW ……後で、しょぼくれた%ANAME(対象)%が帰ってきた
		PRINTFORMW どうやらうまくいかなかったらしい……
	CASE 1
		PRINTFORML しょせん獣であるし、餌で釣るのが一番だろう
		PRINTFORML 「うーん？　それはいい考えかもね」
		PRINTFORML 「ちょっとやってみるね」
		PRINTFORML ……帰ってきた%ANAME(対象)%は、大量の猫を引き連れていた
		PRINTFORML うまくいったらしい……
		CALL COLORPRINT(@"%ANAME(対象)%の武闘が3上昇した", カラー_注意, "L")
		ABL:対象:武闘 += 3
	CASE 2
		PRINTFORML 猫ならまたたびだろう
		PRINTFORML 「うーん？　そうかなぁ」
		PRINTFORML 「まあ、やってみるね」
		PRINTFORML ……後で、酩酊した%ANAME(対象)%が帰ってきた
		PRINTFORMW そりゃそうか……
	CASE 3
		PRINTFORML そもそも猫を仲間にしても戦力になるまい
		PRINTFORML もう少し強そうな動物を仲間にしてはどうだ……
		PRINTFORML 「うーん？　そうかなぁ」
		PRINTFORML 「まあ、やってみるね」
		PRINTFORML ……後で、けがをした%ANAME(対象)%が帰ってきた
		PRINTFORML 熊に声をかけて返り討ちにあったらしい……
		CALL ADD_COOLTIME(対象, 3)
	CASE 4
		PRINTFORML うまくいかないこともあるだろう
		PRINTFORML 諦めたらどうだ……
		PRINTFORML 「まあ、それもそっか」
		PRINTFORML 「今回はやめておこっと」
ENDSELECT

RETURN 1



